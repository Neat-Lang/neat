module neat.formatstring;

macro import std.macro.listcomprehension;
macro import std.macro.quasiquoting;

import neat.array;
import neat.base;
import neat.class_;
import neat.decl;
import neat.either;
import neat.expr;
import neat.struct_;
import neat.types;
import neat.tuples;
import neat.util;
import neat.vectors;

class ASTFormatString : ASTSymbol
{
    ASTSymbol[] parts;

    this(this.parts, this.locRange) { }

    override (Symbol | fail Error) compile(Context context) {
        mut auto context = context;
        auto compiler = context.compiler;
        mut ASTSymbol result = compiler.astStringLiteral("", __RANGE__);
        bool isString(Type type) {
            if (auto array = type.instanceOf(Array))
                return !!array.elementType.instanceOf(Character);
            return false;
        }
        ASTSymbol str(string text) {
            return compiler.astStringLiteral(text, __RANGE__);
        }
        ASTSymbol append(ASTSymbol prev, string text) {
            auto strSym = str(text);
            return compiler.$expr $prev ~ $strSym;
        }
        for (auto part in parts) {
            auto sym = part.compile(context)?.beExpressionImplCall(context, part.locRange)?;
            ASTSymbol astSym = new ASTSymbolHelper(sym);
            if (sym.type.isString) {
                result = compiler.$expr $result ~ $astSym;
            } else if (sym.type.instanceOf(Integer) || sym.type.instanceOf(Short) || sym.type.instanceOf(UByte)) {
                result = compiler.$expr $result ~ itoa($astSym);
            } else if (sym.type.instanceOf(Character)) {
                result = compiler.$expr $result ~ ctoa($astSym);
            } else if (sym.type.instanceOf(Float) || sym.type.instanceOf(Double)) {
                result = compiler.$expr $result ~ ftoa($astSym);
            } else if (sym.type.instanceOf(Long)) {
                result = compiler.$expr $result ~ ltoa($astSym);
            } else if (sym.type.instanceOf(Boolean)) {
                result = compiler.$expr $result ~ btoa($astSym);
            } else if (sym.type.instanceOf(Struct) || sym.type.instanceOf(ClassType) || sym.type.instanceOf(IntfType)) {
                result = compiler.$expr $result ~ $astSym.toString;
            } else if (sym.type.instanceOf(Array)) {
                result = compiler.$expr $result ~ atoa($astSym);
            } else if (sym.type.instanceOf(NullPointer) || sym.type.instanceOf(Pointer)) {
                result = compiler.$expr $result ~ ptoa($astSym);
            } else if (auto identType = sym.type.instanceOf(SymbolIdentifierType)) {
                result = result.append(identType.repr);
            } else if (auto tupleType = sym.type.instanceOf(TupleType)) {
                mut ASTSymbol[] parts = [str("(")];
                for (int i, member in tupleType.members) {
                    if (i) parts ~= str(", ");
                    if (!member.name.empty) {
                        parts ~= str("$(member.name)=");
                    }
                    auto memberExpr = compiler.astIndex(
                        compiler.astIdentifier("that", __RANGE__),
                        compiler.astNumberLiteral(i, __RANGE__), __RANGE__);
                    parts ~= memberExpr;
                }
                parts ~= str(")");
                auto subExpr = new ASTFormatString(parts, __RANGE__);
                auto astPropertyExpr = compiler.astParenPropertyExpression(astSym, subExpr, __RANGE__);
                result = compiler.$expr $result ~ $astPropertyExpr;
            } else if (auto eitherType = sym.type.instanceOf(Either)) {
                mut ASTEitherCaseExprCase[] cases;
                for (k, v in eitherType.types) {
                    ASTSymbol aIdentifier = new ASTIdentifier("a", false, __RANGE__);
                    cases ~= ASTEitherCaseExprCase(
                        __RANGE__,
                        new ASTSymbolHelper(v.type),
                        "a",
                        fail=false,
                        expr=new ASTFormatString([aIdentifier], __RANGE__),
                    );
                }
                auto caseExpr = new ASTEitherCaseExpr(__RANGE__, astSym, cases);
                caseExpr.singleUse = true;
                result = compiler.$expr $result ~ $caseExpr;
            } else if (auto vectorType = sym.type.instanceOf(VectorType)) {
                mut ASTSymbol[] parts = [str("<")];
                for (i in 0 .. vectorType.length) {
                    if (i) parts ~= str(", ");
                    auto memberExpr = compiler.astMember(
                        compiler.astIdentifier("that", __RANGE__),
                        "_$i", __RANGE__);
                    parts ~= memberExpr;
                }
                parts ~= str(">");
                auto subExpr = new ASTFormatString(parts, __RANGE__);
                auto astPropertyExpr = compiler.astParenPropertyExpression(astSym, subExpr, __RANGE__);
                result = compiler.$expr $result ~ $astPropertyExpr;
            } else {
                return this.locRange.fail("Don't know how to format $(sym.type.repr)");
            }
        }
        result = compiler.$expr ({
            import package(compiler).neat.runtime : atoa, btoa, ctoa, ftoa, itoa, ltoa, ptoa; $result;
        });

        return result.compile(context);
    }

    override ASTSymbol quote(Quoter quoter) {
        auto parts = [a.quote(quoter) for a in this.parts];
        auto partsArray = quoter.compiler.astArrayLiteralTypeHint(
            quoter.compiler.astIdentifier("ASTSymbol", __RANGE__),
            parts,
            __RANGE__);

        return quoter.compilerCall("astFormatString", [
            partsArray], this.locRange);
    }
}

