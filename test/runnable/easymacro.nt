module easymacro;

macro import std.macro.assert;
macro import std.macro.easymacro;

void main() {
    mut string marker;
    macro {
        int a = 5;
        auto printIdentifier = compiler.astIdentifier("print", __RANGE__);
        code {
            import std.stdio : print;
            marker ~= "A";
        }
        code {
            $printIdentifier("Printed during runtime.");
            marker ~= "B";
            macro {
                print("  CT value a is $a");
                auto astA = compiler.astNumberLiteral(a, __RANGE__);
                code {
                    auto a = $astA;
                    print("  RT value a is $a");
                    marker ~= "C";
                }
            }
            marker ~= "D";
        }
    }
    assert(marker == "ABCD");
}
