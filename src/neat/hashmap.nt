module neat.hashmap;

macro import package(compiler).std.macro.once;
macro import package(compiler).std.macro.quasiquoting;

import backend.base : Platform, BackendType;
import neat.array;
import neat.base;
import neat.decl;
import neat.either;
import neat.expr;
import neat.formatstring;
import neat.runtime : assert;
import neat.statements;
import neat.struct_;
import neat.tuples;
import neat.types;
import neat.util;
import polyhash;

/**
 * Hashmaps are simpler than arrays: the hashmap value is just a pointer to the hashmap object.
 *
 * (This is because they cannot be sliced.)
 *
 * Hashmap objects have the following layout:
 * {
 *      size_t references;
 *      size_t length; // number of occupied slots
 *      size_t capacity; // number of total slots
 * }
 *
 * It is then followed directly by the hashmap data, which consists of this type:
 *
 * (Key key, Value value | :empty | :gravestone)
 */
class HashMap : Type
{
    // FIXME pending
    Type key, value;

    this(this.key, this.value) {
        this.zeroInitializable = true;
        this.hasElaborateCopyConstructor = true;
    }

    override BackendType emit(Platform platform)
    {
        return platform.voidp;
    }

    override bool same(Type other)
    {
        if (auto otherHashmap = other.instanceOf(HashMap))
            return key.same(otherHashmap.key) && value.same(otherHashmap.value);
        return false;
    }

    override string repr() { return "$(this.value.repr)[$(this.key.repr)]"; }

    override string mangle() { return "hashmap_$(this.key.mangle)_to_$(this.value.mangle)"; }

    override void hash(Hash hash) { hash.adds("hashmap"); this.key.hash(hash); this.value.hash(hash); }

    override (Expression | Error) copy(Context context, Expression source, LocRange locRange)
    {
        auto compiler = context.compiler;
        (nullable Expression | Error) do_(Expression source) {
            mut auto context = context.withNamespace(context.compiler.exprAlias(context.namespace, "source", source));
            auto refCountIncFun = new FunctionDeclaration(
                "neat_runtime_refcount_inc2", new Void, [
                    Parameter.simple("label", new Pointer(new Character)),
                    Parameter.simple("ptr", new Pointer(new Void))]);
            context = context.withNamespace(context.compiler.exprAlias(
                        context.namespace, "neat_runtime_refcount_inc2", refCountIncFun));
            // count up reference
            auto stmt = compiler.$stmt if (cast(void*) source != null) {
                neat_runtime_refcount_inc2("hashmap acquire", cast(size_t*) source);
            };
            auto result = stmt.compile(context)?;
            return compiler.statementExpression(result.statement, source);
        }
        return compiler.exprWithTemporaryExpr(context.getUniqueId, source, &do_)?? else die;
    }

    override nullable Statement endLifetime(Context context, Expression expr)
    {
        return context.compiler.exprStatement(releaseHashmap(context, expr));
    }

    override (nullable Symbol | Error) accessMember(
        Context context, nullable Expression base, string field, Protection protection, LocRange locRange)
    {
        if (auto base = base) {
            if (field == "keys") {
                return keysHashmap(context, this, base);
            }
            if (field == "values") {
                return valuesHashmap(context, this, base);
            }
            if (field == "clear") {
                return clearHashmap(context, this, base);
            }
        }
        return null;
    }

    override (nullable Expression | Error) callMember(
        Context context, nullable Expression base, string field, ASTArgument[] args, Protection protection, LocRange locRange)
    {
        if (auto base = base) {
            if (field == "set") {
                locRange.assert(args.length == 2, () => "hashmap.set() expects key, value")?;
                auto key = args[0].sym.compile(context)?.beExpressionImplCall(context, args[0].locRange)?;
                auto value = args[1].sym.compile(context)?.beExpressionImplCall(context, args[1].locRange)?;
                auto keyConv = expectImplicitConvertTo(context, key, this.key, args[0].locRange)?;
                auto valueConv = expectImplicitConvertTo(context, value, this.value, args[1].locRange)?;
                auto set = setHashmap(context, this, base, keyConv, valueConv, locRange)?;
                return context.compiler.statementExpression(set, context.compiler.voidLiteral);
            }
            if (field == "get") {
                locRange.assert(args.length == 2, () => "hashmap.get() expects two parameters, key and default")?;
                auto key = args[0].sym.compile(context)?.beExpressionImplCall(context, args[0].locRange)?;
                auto deflt = args[1].sym.compile(context)?.beExpressionImplCall(context, args[1].locRange)?;
                return getHashmapDefault(context, locRange, this, base, key, deflt, insert=false);
            }
            if (field == "require") {
                locRange.assert(args.length == 2, () => "hashmap.require() expects two parameters, key and default")?;
                auto key = args[0].sym.compile(context)?.beExpressionImplCall(context, args[0].locRange)?;
                auto deflt = args[1].sym.compile(context)?.beExpressionImplCall(context, args[1].locRange)?;
                return getHashmapDefault(context, locRange, this, base, key, deflt, insert=true);
            }
            if (field == "has") {
                locRange.assert(args.length == 1, () => "hashmap.has() expects one parameter, key")?;
                auto key = args[0].sym.compile(context)?.beExpressionImplCall(context, args[0].locRange)?;
                return hasKeyHashmap(context, this, base, key);
            }
            if (field == "resize") {
                auto newsize = args[0].sym.compile(context)?.beExpressionImplCall(context, args[0].locRange)?;
                return resizeHashmap(context, this, base, newsize);
            }
            if (field == "remove") {
                locRange.assert(args.length == 1, () => "hashmap.remove() expects one parameter, key")?;
                auto key = args[0].sym.compile(context)?.beExpressionImplCall(context, args[0].locRange)?;
                return removeKeyHashmap(context, this, base, key);
            }
            if (field == "clear") {
                locRange.assert(args.length == 0, () => "hashmap.clear() expects no parameters")?;
                return clearHashmap(context, this, base);
            }
        } else {
            if (field == "allocate") {
                locRange.assert(args.length == 1, () => "hashmap.allocate() expects one parameter, size")?;
                auto size = args[0].sym.compile(context)?.beExpressionImplCall(context, args[0].locRange)?;
                auto sizeT = expectImplicitConvertTo(context, size, context.nativeWordType, args[0].locRange)?;
                return allocateHashmap(context, this, sizeT);
            }
        }
        return null;
    }

    override (nullable Expression | Error) index(
        Context context, Expression base, ASTSymbol index, LocRange locRange)
    {
        auto indexExpr = index.compile(context)?.beExpressionImplCall(context, index.locRange)?;
        auto indexConv = expectImplicitConvertTo(context, indexExpr, this.key, locRange)?;
        return getHashmap(context, this, base, indexConv);
    }

    override (nullable Statement | Error) indexAssignment(
        Context context, Expression base, ASTSymbol index, Expression value, LocRange locRange)
    {
        auto indexExpr = index.compile(context)?.beExpressionImplCall(context, index.locRange)?;
        auto indexConv = expectImplicitConvertTo(context, indexExpr, this.key, locRange)?;
        auto valueConv = expectImplicitConvertTo(context, value, this.value, locRange)?;
        return setHashmap(context, this, base, indexConv, valueConv, locRange);
    }
}

private ASTSymbol hashWrap(Context context, HashMap hashmap, ASTSymbol value) {
    auto keyType = new ASTSymbolHelper(hashmap.key), valueType = new ASTSymbolHelper(hashmap.value);
    return context.compiler.$expr ({
        0; // remove at your own risk
        alias Key = $keyType;
        alias Value = $valueType;
        struct HashMapBase {
            size_t references;
            size_t length;
            size_t capacity;
        }
        alias HashMapRow = (Key key, Value value | :empty | :gravestone);
        $value;
    });
}

private Expression allocateHashmap(Context context, HashMap hashmap, Expression size) {
    Parameter[] params = [
        Parameter.simple("size", context.nativeWordType)];

    ASTStatement body_() {
        auto expr = context.compiler.$expr ({
            auto bytesize = sizeof(HashMapBase) + sizeof(HashMapRow) * size;
            auto ret = neat_runtime_alloc(bytesize);
            auto base = cast(HashMapBase*) ret;
            base.references = 1;
            base.length = 0;
            base.capacity = size;
            auto rows = cast(HashMapRow*) &base[1];
            for (auto i in 0 .. size)
                rows[i] = :empty;
            cast(Value[Key]) base;
        });
        return context.compiler.(
            astExpressionStmt(astReturn(hashWrap(context, hashmap, expr))));
    }

    auto fn = context.compiler.createRuntimeFunction(
        context, "hashmap_allocate", hashmap, params, &body_);

    return context.compiler.mkCall(fn, [size]);
}

private Expression resizeHashmap(Context context, HashMap hashmap, Expression base, Expression newsize) {
    (Expression | Error) do_(Expression base) {
        return plainResizeHashmap(context, hashmap, base, newsize);
    }
    context.compiler.consume(context, base, &do_).case {
        Expression e: return e;
        Error err: assert(false);
    }
}

private Expression plainResizeHashmap(Context context, HashMap hashmap, Expression base, Expression newsize) {
    Parameter[] params = [
        Parameter.simple("base", hashmap),
        Parameter.simple("newsize", context.nativeWordType)];

    ASTStatement body_() {
        auto expr = context.compiler.$expr ({
            auto data = cast(HashMapBase*) base;
            auto rows = cast(HashMapRow*) &data[1];
            // FIXME
            alias T = Value[Key];
            mut auto newHashmap = T.allocate(newsize);
            for (i in 0 .. data.capacity) {
                0; // remove at your peril
                rows[i].case {
                    (:empty): continue;
                    (:gravestone): continue;
                    (Key key, Value value): newHashmap.set(key, value);
                }
            }
            newHashmap;
        });
        return context.compiler.(
            astExpressionStmt(astReturn(hashWrap(context, hashmap, expr))));
    }

    auto fn = context.compiler.createRuntimeFunction(
        context, "hashmap_resize", hashmap, params, &body_);

    return context.compiler.mkCall(fn, [base, newsize]);
}

private (Statement | Error) setHashmap(
    Context context, HashMap hashmap, Expression base, Expression key, Expression value, LocRange locRange)
{
    auto baseRef = base.instanceOf(Reference)?
        else return locRange.fail("target of hashmap assignment must be mutable");
    locRange.assert(baseRef.mutable, () => "target of hashmap assignment must be mutable")?;
    // we temporize value first thing, because evaluating value may change the addresss of the hashmap.
    (nullable Statement | Error) do3(Expression value) {
        // Then key, same reason.
        (nullable Statement | Error) do2(Expression key) {
            // mark value for consumption.
            Statement do1(Expression value) {
                return plainSetHashmap(context, hashmap, baseRef, key, value);
            }
            return context.compiler.consumeStmt(context, value, &do1);
        }
        return context.compiler.stmtWithTemporaryExpr(context.getUniqueId, key, &do2);
    }
    return context.compiler.stmtWithTemporaryExpr(context.getUniqueId, value, &do3).case(
        nullable Statement st: st? else die, Error err: err);
}

private Statement plainSetHashmap(Context context, HashMap hashmap, Reference base, Expression key, Expression value) {
    Parameter[] params = [
        Parameter.simple("base", hashmap),
        Parameter.simple("key", hashmap.key),
        Parameter.simple("value", hashmap.value)];

    ASTStatement body_() {
        auto expr = context.compiler.$expr ({
            // FIXME
            alias T = Value[Key];
            auto hash = __hash(key);
            mut auto data = cast(HashMapBase*) base;
            if (!data) {
                auto newHashmap = T.allocate(4);
                data = cast(HashMapBase*) newHashmap; // storing in a pointer
            } else if (data.length + 1 >= data.capacity / 2) {
                mut size_t newsize = data.capacity * 2;
                if (newsize < 4) newsize = 4;
                auto resized = base.resize(newsize);
                data = cast(HashMapBase*) resized;
            } else {
                // we'll return data, so make a gift
                data.references += 1;
            }
            auto rows = cast(HashMapRow*) &data[1];
            for (i in 0 .. data.capacity) {
                size_t index = (i + hash) % data.capacity; // FIXME wraparound test instead of %
                rows[index].case {
                    (:empty): {
                        data.length += 1;
                        rows[index] = (key, value);
                        return cast(T) data;
                    }
                    (:gravestone): {
                        data.length += 1;
                        rows[index] = (key, value);
                        return cast(T) data;
                    }
                    (Key key, Value value) existingEntry: {
                        if (key == existingEntry.key)
                        {
                            rows[index] = (key, value);
                            return cast(T) data;
                        }
                        continue;
                    }
                }
            }
            int* ip = null; *ip = *ip;
            // TODO
            // assert(false); // No space even after resize.
            0; // expr result value. TODO void literal
        });
        return new ASTExprStatement(hashWrap(context, hashmap, expr));
    }

    (nullable Statement | Error) do_(Expression callTemp) {
        with (context.compiler) {
            auto assign = assignStatement(base, callTemp);
            if (auto endLifetime = hashmap.endLifetime(context, base)) {
                return sequence(endLifetime, assign);
            }
            return assign;
        }
    }

    with (context.compiler) {
        auto fn = createRuntimeFunction(
            context, "hashmap_set", hashmap, params, &body_);

        auto call = mkCall(fn, [base, key, value]);
        stmtWithTemporaryExpr(context.getUniqueId, call, &do_).case {
            nullable Statement stmt: return stmt? else die;
            Error err: assert(false);
        }
    }
}

private Expression getHashmap(Context context, HashMap hashmap, Expression base, Expression key) {
    Parameter[] params = [
        Parameter.simple("base", hashmap),
        Parameter.simple("key", hashmap.key)];

    ASTStatement body_() {
        auto expr = context.compiler.$expr ({
            import package(compiler).neat.runtime : assert, print;

            mut auto data = cast(HashMapBase*) base;
            if (!data) {
                print("getting key from null hashmap");
                assert(false);
            }
            auto rows = cast(HashMapRow*) &data[1];
            // print("get $key: debug $(__hash(key)) $(__hash(key))");
            mut size_t index = __hash(key) % data.capacity;
            for (i in 0 .. data.capacity) {
                // print("get: probe index $index of $(data.capacity): key $key");
                rows[index].case {
                    (:empty): {
                        print("key not found at hash location");
                        assert(false);
                    }
                    (:gravestone): {}
                    (Key key, Value value) existingEntry: {
                        // print("get: compare existing key $(existingEntry.key)");
                        if (key == existingEntry.key) return existingEntry.value;
                    }
                }
                index += 1;
                if (index == data.capacity) index = 0;
            }
            print("key not found anywhere in hashmap");
            assert(false);
            0; // expr result value. TODO void literal
        });
        return new ASTExprStatement(hashWrap(context, hashmap, expr));
    }

    auto fn = context.compiler.createRuntimeFunction(
        context, "hashmap_get", hashmap.value, params, &body_);

    return context.compiler.mkCall(fn, [base, key]);
}

private Expression removeKeyHashmap(Context context, HashMap hashmap, Expression base, Expression key) {
    Parameter[] params = [
        Parameter.simple("base", hashmap),
        Parameter.simple("key", hashmap.key)];

    ASTStatement body_() {
        auto expr = context.compiler.$expr ({
            auto hash = __hash(key);
            mut auto data = cast(HashMapBase*) base;
            if (!data) return false;
            auto rows = cast(HashMapRow*) &data[1];
            for (i in 0 .. data.capacity) {
                // FIXME wraparound test instead of %
                size_t index = (i + hash) % data.capacity;
                rows[index].case {
                    (:empty): return false;
                    (:gravestone): {}
                    (Key key, Value value) entry: {
                        if (key == entry.key) {
                            data.length -= 1;
                            rows[index] = :gravestone;
                            return true;
                        }
                    }
                }
            }
            int* ip = null; *ip = *ip;
            // TODO
            // assert(false); // hashmap was completely full, this should never happen
            0; // expr result value. TODO bottom literal
        });
        return new ASTExprStatement(hashWrap(context, hashmap, expr));
    }

    auto fn = context.compiler.createRuntimeFunction(
        context, "hashmap_remove", new Boolean, params, &body_);

    return context.compiler.mkCall(fn, [base, key]);
}

private Expression clearHashmap(Context context, HashMap hashmap, Expression base) {
    Parameter[] params = [
        Parameter.simple("base", hashmap)];

    ASTStatement body_() {
        auto expr = context.compiler.$expr ({
            // FIXME
            alias T = Value[Key];
            mut auto data = cast(HashMapBase*) base;
            if (!data) return;
            auto rows = cast(HashMapRow*) &data[1];
            for (i in 0 .. data.capacity) {
                rows[i] = :empty;
            }
            data.length = 0;
            // TODO bottom expression
            0;
        });
        return new ASTExprStatement(hashWrap(context, hashmap, expr));
    }

    auto fn = context.compiler.createRuntimeFunction(
        context, "hashmap_clear", new Void, params, &body_);

    return context.compiler.mkCall(fn, [base]);
}

private Expression hasKeyHashmap(Context context, HashMap hashmap, Expression base, Expression key) {
    Parameter[] params = [
        Parameter.simple("base", hashmap),
        Parameter.simple("key", hashmap.key)];

    ASTStatement body_() {
        auto expr = context.compiler.$expr ({
            import package(compiler).neat.runtime : assert, print;

            // FIXME
            alias T = Value[Key];
            mut auto data = cast(HashMapBase*) base;
            if (!data) return false;
            auto rows = cast(HashMapRow*) &data[1];
            // print("has: debug $(__hash(key)) $(__hash(key))");
            mut size_t index = __hash(key) % data.capacity;
            for (i in 0 .. data.capacity) {
                // print("has: probe index $index of $(data.capacity): key $key");
                rows[index].case {
                    (:empty): return false;
                    (:gravestone): {}
                    (Key key, Value value) existingEntry: {
                        // print("has: compare existing key $(existingEntry.key)");
                        if (key == existingEntry.key) return true;
                    }
                }
                index += 1;
                if (index == data.capacity) index = 0;
            }
            print("cannot check for key: hashmap overloaded");
            assert(false);
            0; // expr result value. TODO bottom literal
        });
        return new ASTExprStatement(hashWrap(context, hashmap, expr));
    }

    auto fn = context.compiler.createRuntimeFunction(
        context, "hashmap_has", new Boolean, params, &body_);

    return context.compiler.mkCall(fn, [base, key]);
}

public (Expression | Error) getHashmapDefault(
    Context context, LocRange locRange, HashMap hashmap, Expression base, Expression key, Expression deflt, bool insert)
{
    auto astValue = new ASTSymbolHelper(hashmap.value);
    auto astBase = new ASTSymbolHelper(base);
    auto astKey = new ASTSymbolHelper(key);
    auto astDeflt = new ASTSymbolHelper(deflt);
    mut ASTStatement astElse = context.compiler.$stmt ret = $astDeflt;;
    if (insert) {
        auto baseRef = base.instanceOf(Reference);
        locRange.assert(!!baseRef && baseRef.mutable, () => "target of hashmap assignment must be mutable")?;
        astElse = context.compiler.$stmt {
            ret = $astDeflt;
            $astBase[key] = ret;
        };
    }

    // FIXME single lookup
    auto expr = context.compiler.$expr ({
        mut uninitialized $astValue ret;
        auto key = $astKey;
        if ($astBase.has(key)) ret = $astBase[key];
        else $stmt astElse;
        ret;
    });
    return expr.compile(context)?.beExpression?;
}

public Expression releaseHashmap(Context context, Expression value) {
    auto hashmap = value.type.instanceOf(HashMap)?
        else __RANGE__.die("hashmap expected");
    Parameter[] params = [Parameter.simple("value", hashmap)];

    ASTStatement body_() {
        auto expr = context.compiler.$expr ({
            if (cast(void*) value == null) return;
            if (!neat_runtime_refcount_dec2("hashmap release", cast(size_t*) value))
                return;
            auto base = cast(HashMapBase*) value;
            auto rows = cast(HashMapRow*) &base[1];
            for (i in 0 .. base.capacity) {
                rows[i].case {
                    (:empty): continue;
                    (:gravestone): continue;
                    (Key key, Value value): {
                        __destroy(key);
                        __destroy(value);
                    }
                }
                0; // Remove at your peril. (FIXME typed array literals or implcast array literals)
            }
            free(cast(void*) value);
            0; // expr result value. TODO void literal
        });
        return new ASTExprStatement(hashWrap(context, hashmap, expr));
    }

    auto fn = context.compiler.createRuntimeFunction(context, "hashmap_release", new Void, params, &body_);

    return context.compiler.mkCall(fn, [value]);
}

private Expression keysHashmap(Context context, HashMap hashmap, Expression base) {
    ASTStatement body_() {
        auto expr = context.compiler.$expr ({
            if (cast(void*) value == null) {
                Key[] ret; return ret;
            }
            auto base = cast(HashMapBase*) value;
            auto rows = cast(HashMapRow*) &base[1];
            auto ret = new Key mut[](base.length);
            mut int k = 0;
            for (i in 0 .. base.capacity) {
                rows[i].case {
                    (:empty): continue;
                    (:gravestone): continue;
                    (Key key, Value value): {
                        ret[k] = key;
                        k += 1;
                    }
                }
                0;
            }
            // assert(k == base.length);
            if (k != base.length) { int* ip; *ip = *ip; }
            ret.freeze;
        });
        return context.compiler.(
            astExpressionStmt(astReturn(hashWrap(context, hashmap, expr))));
    }

    Parameter[] params = [Parameter.simple("value", hashmap)];
    auto fn = context.compiler.createRuntimeFunction(
        context, "hashmap_keys", new Array(hashmap.key, context.platform), params, &body_);

    return context.compiler.mkCall(fn, [base]);
}

private Expression valuesHashmap(Context context, HashMap hashmap, Expression base) {
    ASTStatement body_() {
        auto expr = context.compiler.$expr ({
            if (cast(void*) value == null) {
                Value[] ret; return ret;
            }
            auto base = cast(HashMapBase*) value;
            auto rows = cast(HashMapRow*) &base[1];
            auto ret = new Value mut[](base.length);
            mut int k = 0;
            for (i in 0 .. base.capacity) {
                rows[i].case {
                    :empty: continue;
                    :gravestone: continue;
                    (Key key, Value value): {
                        ret[k] = value;
                        k += 1;
                    }
                }
                0;
            }
            // assert(k == base.length);
            if (k != base.length) {
                print("logic error: $k results, but base length was tracked at $(base.length)");
                int* ip;
                *ip = *ip;
            }
            ret.freeze;
        });
        return context.compiler.(
            astExpressionStmt(astReturn(hashWrap(context, hashmap, expr))));
    }

    Parameter[] params = [Parameter.simple("value", hashmap)];
    auto fn = context.compiler.createRuntimeFunction(
        context, "hashmap_values", new Array(hashmap.value, context.platform), params, &body_);

    return context.compiler.mkCall(fn, [base]);
}

// TODO just merge this completely with ASTIndexAccess.
class ASTHashMap : ASTSymbol
{
    ASTSymbol key, value;

    this(this.key, this.value, this.locRange=__CALLER__) { }

    override (Symbol | Error) compile(Context context) {
        auto key = this.key.compile(context)?;
        if (auto keyType = key.isType(this.key.locRange)?) {
            auto value = this.value.compile(context)?.beType(this.value.locRange)?;
            return new HashMap(key=keyType, value=value);
        } else if (auto keyExpr = key.isExpression(this.key.locRange)?) {
            return context.compiler.astIndex(base=value, index=this.key, locRange=this.locRange)
                .compile(context);
        } else {
            return this.key.locRange.fail(
                "index expression must be either type (hashmap) or value (index access)");
        }
    }

    override string repr() => "$value[$key] (AA)";
}
