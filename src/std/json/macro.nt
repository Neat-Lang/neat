module std.json.macro;

macro import std.macro.assert;
macro import std.macro.listcomprehension;

import package(compiler).neat.base;
import package(compiler).neat.parser;

import std.json;

class JsonSyntax : Macro
{
    this() { }
    override void apply(MacroArgs args) {
        auto args = args.instanceOf(ParseExpressionBaseArgs);
        if (args) {
            args.symbol = this.parse(args.parser, args.lexicalContext);
        }
    }

    (nullable ASTSymbol | fail Error) parse(Parser parser, LexicalContext lexicalContext)
    {
        auto loc = parser.loc();
        parser.begin;
        if (!parser.acceptIdentifier("JSONValue")? || !parser.accept("(")?)
        {
            parser.revert;
            return null;
        }
        auto value = parseJSONValueMacro(parser, lexicalContext)?;
        parser.expect(")")?;
        parser.commit;
        return value;
    }
}

// TODO correct ranges
private (ASTSymbol | fail Error) parseJSONValueMacro(Parser parser, LexicalContext lexicalContext) {
    if (parser.accept("\"")?) {
        auto str = parseStringLiteral(parser)?;
        return lexicalContext.compiler.astStringLiteral(str, __RANGE__)
            .astJSONValue(lexicalContext.compiler);
    }
    parser.parseJsonNumber?.case {
        (:failure): {}
        int value: return lexicalContext.compiler.astNumberLiteral(value, __RANGE__)
            .astJSONValue(lexicalContext.compiler);
    }
    if (parser.accept("true")?) {
        return lexicalContext.compiler.astIdentifier("true", __RANGE__)
            .astJSONValue(lexicalContext.compiler);
    }
    if (parser.accept("false")?) {
        return lexicalContext.compiler.astIdentifier("false", __RANGE__)
            .astJSONValue(lexicalContext.compiler);
    }
    if (parser.accept("[")?) {
        mut ASTSymbol[] entries;
        if (!parser.accept("]")?) while (true) {
            entries ~= parseJSONValueMacro(parser, lexicalContext)?;
            if (parser.accept("]")?) break;
            parser.expect(",")?;
        }
        with (lexicalContext.compiler) {
            auto literal = astArrayLiteralTypeHint(
                astIdentifier("JSONValue", __RANGE__), entries, __RANGE__);
            return literal.astJSONValue(lexicalContext.compiler);
        }
    }
    if (parser.accept("{")?) {
        mut ASTSymbol[] entries;
        if (!parser.accept("}")?) while (true) {
            parser.expect("\"")?;
            auto key = parseStringLiteral(parser)?;
            parser.expect(":")?;
            auto value = parseJSONValueMacro(parser, lexicalContext)?;

            with (lexicalContext.compiler) {
                entries ~= astTupleLiteral([
                    astStringLiteral(key, __RANGE__),
                    value,
                ], __RANGE__);
            }

            if (parser.accept("}")?) break;
            parser.expect(",")?;
        }
        with (lexicalContext.compiler) {
            auto type = astTupleType([
                ("key", astIdentifier("string", __RANGE__)),
                ("value", astIdentifier("JSONValue", __RANGE__)),
            ], __RANGE__);
            auto literal = astArrayLiteralTypeHint(type, entries, __RANGE__);
            return literal.astJSONValue(lexicalContext.compiler);
        }
    }
    // Maybe it's a Neat expression?`(Let's do some macro magic.)
    if (auto expr = lexicalContext.compiler.parseExpression(parser, lexicalContext)?) {
        return expr.astJSONValue(lexicalContext.compiler);
    }
    return parser.fail("unexpected input: " ~ parser.text);
}

private ASTSymbol astJSONValue(ASTSymbol symbol, CompilerBase compiler) {
    with (compiler) {
        return astCall(astIdentifier("__jv", __RANGE__), [symbol], __RANGE__);
    }
}

void jsonMacro(MacroState macroState)
{
    macroState.addMacro(new JsonSyntax);
}

macro(jsonMacro);

unittest
{
    int five = 5;
    auto value = JSONValue({"Hello": "World", "five": five, "array": [6]});
    assert(value.toString == "{\"Hello\": \"World\", \"five\": 5, \"array\": [6]}");
}
