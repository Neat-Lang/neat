module std.argparse;

macro import std.macro.assert;
macro import std.macro.listcomprehension;
import std.error;

/**
 * Parse command-line arguments and return matched arguments or errors.
 */
abstract class ArgParser
{
    // TODO public_mut?
    public ArgInfo[string] longArgs;

    public ArgInfo[string] shortArgs;

    public ArgInfo[] args;

    int delegate(string, string[], ArgResult)[] specialRules;

    /**
     * Add a command-line argument.
     * Typical usage: `add("h", "help", :noArg, "Print this help page.");
     */
    void add(string shortname, string longname, ArgKind kind, (string | :hidden) description) {
        auto argInfo = ArgInfo(shortname, longname, kind, description);
        longArgs[longname] = argInfo;
        shortArgs[shortname] = argInfo;
        args ~= argInfo;
    }

    /**
     * Add a command-line argument without a short form.
     * Typical usage: `add("help", :noArg, "Print this help page.");
     */
    void add(string name, ArgKind kind, (string | :hidden) description) {
        if (name.length == 1) {
            auto argInfo = ArgInfo(shortname=name, "", kind, description);
            shortArgs[name] = argInfo;
            args ~= argInfo;
        } else {
            auto argInfo = ArgInfo("", longname=name, kind, description);
            longArgs[name] = argInfo;
            args ~= argInfo;
        }
    }

    /**
     * Add a special rule. The callback will be invoked with the current
     * argument, the remaining arguments, and the result object to fill out.
     * It is expected to return the number of arguments consumed, including
     * the current argument.
     */
    void add(int delegate(string, string[], ArgResult) dg) {
        specialRules ~= dg;
    }

    /**
     * Parse a list of command-line arguments. If a string is returned,
     * the command-line arguments did not match the defined arguments.
     */
    abstract (ArgResult | Error) parse(mut string[] args);
}

unittest
{
    auto parser = new UnixArgParser;
    parser.add("h", "help", :noArg, "Print this help page");

    with (parser.parse(null)
        .case(Error: assert(false)))
    {
        assert(remaining.empty);
    }
}

unittest
{
    auto parser = new UnixArgParser;
    parser.add("h", "help", :noArg, "Print this help page");

    with (parser.parse(["foo"])
        .case(Error: assert(false)))
    {
        assert(remaining == ["foo"]);
    }
}

unittest
{
    auto parser = new UnixArgParser;
    parser.add("f", "foo", :intArg, "Foo arg");

    parser.parse(["-f"])
        .case(ArgResult: assert(false))
        .message.assertEqual("Flag '-f' missing parameter");

    parser.parse(["--foo"])
        .case(ArgResult: assert(false))
        .message.assertEqual("Flag '--foo' missing parameter");

    parser.parse(["-f", "x"])
        .case(ArgResult: assert(false))
        .message.assertEqual("Flag '-f' expected integer parameter");

    with (parser.parse(["-f", "5"])
        .case(Error: assert(false)))
    {
        assert(has("foo"));
        assert(remaining.empty);
    }
}

unittest
{
    auto parser = new UnixArgParser;
    parser.add("f", "foo", :noArg, :hidden);
    parser.add("b", "bar", :noArg, :hidden);

    with (parser.parse(["-fb"]).case(Error: assert(false))) {
        assert(get("foo") == "");
        assert(get("bar") == "");
        assert(remaining.empty);
    }
}

unittest
{
    auto parser = new GccArgParser;
    parser.add("f", "foo", :noArg, :hidden);
    parser.add("b", "bar", :noArg, :hidden);

    parser.parse(["-fb"])
        .case(ArgResult: assert(false))
        .message.assertEqual("Flag '-f' expected no parameter");
}

unittest
{
    auto parser = new UnixArgParser;
    with (parser.parse(["foo", "--", "bar"])
        .case(Error: assert(false)))
    {
        assert(remaining == ["foo"]);
        assert(extra == ["bar"]);
    }
}

unittest
{
    auto parser = new GccArgParser;
    parser.add("L", :multiArgs, :hidden);
    with (parser.parse(["-L-lpthreads"])
        .case(Error: assert(false)))
    {
        assert(getMultiple("L") == ["-lpthreads"]);
    }
}

unittest
{
    auto parser = new UnixArgParser;
    int blaAction(string arg, string[] args, ArgResult result) {
        if (arg == "bla") {
            result.foundArgs["bla"] = "";
            // arg consumed
            return 1;
        }
        // no match
        return 0;
    }
    parser.add(new &blaAction);
    with (parser.parse(["bla"])
        .case(Error: assert(false)))
    {
        assert(get("bla") == "");
        assert(remaining.empty);
    }
}

private void assertEqual(string a, string b) => assert(a == b);

/**
 * The kind of a command-line argument. This determines how many further
 * arguments it consumes.
 */
alias ArgKind = (
    :noArg |
    :intArg |
    :stringArg |
    :multiArgs
);

/**
 * Information about a defined command-line argument.
 */
struct ArgInfo
{
    string shortname;

    string longname;

    ArgKind kind;

    string longOrShort() => longname if !longname.empty else shortname;

    (string | :hidden) description;
}

/**
 * UNIX style:
 *
 * * `--foo bar`
 * * `-fx` is `-f, -x`
 */
class UnixArgParser : ArgParser
{
    public this() { }

    override (ArgResult | Error) parse(mut string[] args) {
        import std.string : startsWith, atoi;
        auto result = new ArgResult(this);
        (void | Error) parseFlagImpl(ArgInfo argInfo, string flagInfo) {
            if (argInfo.kind == :noArg) {
                result.foundArgs[argInfo.longOrShort] = null;
                return;
            }
            if (args.empty) {
                return new Error("Flag $flagInfo missing parameter");
            }
            auto parameter = args[0];
            args = args[1 .. $];
            if (parameter.startsWith("-")) {
                return new Error("Flag $flagInfo missing parameter");
            }
            argInfo.kind.case {
                :stringArg:
                    result.foundArgs[argInfo.longOrShort] = parameter;
                :intArg:
                    int asInt = atoi(parameter);
                    if ("$asInt" != parameter) {
                        return new Error("Flag $flagInfo expected integer parameter");
                    }
                    result.foundArgs[argInfo.longOrShort] = parameter;
                :multiArgs:
                    result.multiArgs.require(argInfo.longOrShort, null);
                    result.multiArgs[argInfo.longOrShort] ~= parameter;
                :noArg:
                    assert(false);
            }
        }
        (void | Error) parseShortFlag(string flag, bool lastFlag) {
            auto flagInfo = "'-$flag'";
            if (!this.shortArgs.has(flag))
                return new Error("Unknown flag $flagInfo");
            auto argInfo = this.shortArgs[flag];
            if (argInfo.kind != :noArg && !lastFlag) {
                return new Error("Flag $flagInfo with parameter must come last");
            }
            return parseFlagImpl(argInfo, flagInfo);
        }
        (void | Error) parseLongFlag(string flag) {
            auto flagInfo = "'--$flag'";
            if (!this.longArgs.has(flag))
                return new Error("Unknown flag $flagInfo");
            return parseFlagImpl(this.longArgs[flag], flagInfo);
        }
        while (!args.empty) {
            auto arg = args[0];
            args = args[1 .. $];
            // TODO named break/continue
            mut bool continue_outer = false;
            for (rule in specialRules) {
                int argsConsumed = rule(arg, args, result);
                if (argsConsumed == 0) continue;
                args = args[argsConsumed - 1 .. $];
                continue_outer = true;
                break;
            }
            if (continue_outer) continue;

            if (arg == "--") {
                result.extra = args;
                break;
            } else if (arg.startsWith("--")) {
                parseLongFlag(arg[2 .. $])?;
            } else if (arg.startsWith("-") && arg != "-") {
                for (i in 1 .. arg.length) {
                    parseShortFlag(arg[i .. i + 1], lastFlag=(i == arg.length - 1))?;
                }
            } else {
                result.remaining ~= arg;
            }
        }
        return result;
    }
}

/**
 * GCC style:
 *
 * * `--foo bar`, `--foo=bar`, `-foo=bar`
 * * `-fx` is `--foo=x`
 */
class GccArgParser : ArgParser
{
    public this() { }

    override (ArgResult | Error) parse(mut string[] args) {
        import std.string : startsWith, atoi;
        auto result = new ArgResult(this);
        (void | Error) parseArgParameter(ArgInfo argInfo, string parameter, string flagInfo) {
            argInfo.kind.case {
                :stringArg:
                    result.foundArgs[argInfo.longOrShort] = parameter;
                :intArg:
                    int asInt = atoi(parameter);
                    if ("$asInt" != parameter) {
                        return new Error("Flag $flagInfo expected integer parameter");
                    }
                    result.foundArgs[argInfo.longOrShort] = parameter;
                :multiArgs:
                    result.multiArgs.require(argInfo.longOrShort, null);
                    result.multiArgs[argInfo.longOrShort] ~= parameter;
                :noArg:
                    assert(false);
            }
        }
        (void | Error) parseArg(ArgInfo argInfo, string flagInfo) {
            if (argInfo.kind == :noArg) {
                result.foundArgs[argInfo.longOrShort] = null;
                return;
            }
            if (args.empty) {
                return new Error("Flag $flagInfo missing parameter");
            }
            auto parameter = args[0];
            args = args[1 .. $];
            if (parameter.startsWith("-")) {
                return new Error("Flag $flagInfo missing parameter");
            }
            return parseArgParameter(argInfo, parameter, flagInfo);
        }
        // --foo
        (void | Error) parseLongFlag(string flag) {
            auto flagInfo = "'--$flag'";
            if (!this.longArgs.has(flag))
                return new Error("Unknown flag $flagInfo");
            return parseArg(this.longArgs[flag], flagInfo);
        }
        // -foo=bar, --foo=bar
        (void | Error) parseAssignedFlag(string flag, string value, string flagInfo) {
            if (!this.longArgs.has(flag))
                return new Error("Unknown flag $flagInfo");
            if (this.longArgs[flag].kind == :noArg) {
                return new Error("Flag $flagInfo expected no parameter");
            }
            return parseArgParameter(this.longArgs[flag], value, flagInfo);
        }
        // -fbar
        (void | Error) parseFlag(string arg) {
            if (this.longArgs.has(arg)) {
                auto flagInfo = "'-$arg'";
                return parseArg(this.longArgs[arg], flagInfo);
            }
            auto prefix = arg[0 .. 1];
            if (this.shortArgs.has(prefix)) {
                auto flagInfo = "'-$prefix'";
                if (arg.length > 1) {
                    // -j32
                    if (this.shortArgs[prefix].kind == :noArg) {
                        return new Error("Flag $flagInfo expected no parameter");
                    }
                    return parseArgParameter(this.shortArgs[prefix], arg[1 .. $], flagInfo);
                }
                // -j
                return parseArg(this.shortArgs[prefix], flagInfo);
            }
        }
        while (!args.empty) {
            auto arg = args[0];
            args = args[1 .. $];
            // TODO named break/continue
            mut bool continue_outer = false;
            for (rule in specialRules) {
                int argsConsumed = rule(arg, args, result);
                if (argsConsumed == 0) continue;
                args = args[argsConsumed - 1 .. $];
                continue_outer = true;
                break;
            }
            if (continue_outer) continue;

            if (arg == "--") {
                result.extra = args;
                break;
            } else if (arg.startsWith("--")) {
                int pos = [first cast(int) i for i, c in arg where c == '=' else -1];
                if (pos != -1) {
                    // --foo=bar
                    auto flag = arg[2 .. pos];
                    parseAssignedFlag(flag, arg[pos + 1 .. $], "'--$flag'")?;
                } else {
                    // --foo bar
                    parseLongFlag(arg[2 .. $])?;
                }
            } else if (arg.startsWith("-") && arg != "-") {
                int pos = [first cast(int) i for i, c in arg where c == '=' else -1];
                if (pos != -1) {
                    // -foo=bar
                    auto flag = arg[1 .. pos];
                    parseAssignedFlag(flag, arg[pos + 1 .. $], "'-$flag'")?;
                } else {
                    // -j8
                    parseFlag(arg[1 .. $])?;
                }
            } else {
                result.remaining ~= arg;
            }
        }
        return result;
    }
}

/**
 * The result of successfully parsing command-line arguments.
 */
class ArgResult
{
    private ArgParser parser;

    // TODO package
    public string[string] foundArgs;

    public string[][string] multiArgs;

    /// Non-flag arguments in the commandline
    public string[] remaining;

    /// Arguments after the --
    public string[] extra;

    this(this.parser) {}

    /// Returns whether an argument was matched.
    bool has(string arg) => this.foundArgs.has(arg) || this.multiArgs.has(arg);

    /// Returns the parameter of a matched argument that took a parameter.
    string get(string arg) {
        if (!this.foundArgs.has(arg)) {
            print("wrong result key '$arg'");
            assert(false);
        }
        return this.foundArgs[arg];
    }

    /// Returns all parameters of an argument that could match multiple times.
    string[] getMultiple(string arg) {
        if (!this.multiArgs.has(arg)) {
            print("wrong result key '$arg'");
            assert(false);
        }
        return this.multiArgs[arg];
    }

    /// Helper to manually set an argument to a value.
    void set(string arg, string value) { this.foundArgs[arg] = value; }
}
