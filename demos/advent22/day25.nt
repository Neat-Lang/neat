module day25;

macro import std.macro.assert;
macro import std.macro.listcomprehension;

import std.algorithm;
import std.stdio;

void main()
{
    auto sum = [sum a for a in stdin.byLine.map(a => decodeSnafu(a.dup))];
    print("sum is $sum, snafu $(sum.encodeSnafu)");
}

long decodeSnafu(char mut[] s) {
    mut long factor = 1;
    mut long res;
    for (i in 0 .. s.length) {
        res += factor * s[$ - 1 - i].case(
            '0': 0,
            '1': 1,
            '2': 2,
            '-': -1,
            '=': -2);
        factor *= 5;
    }
    return res;
}

string encodeSnafu(long l) {
    mut int digits = 1;
    while (l > decodeSnafu(['2' for i in 0 .. digits].dup)
        || l < decodeSnafu(['=' for i in 0 .. digits].dup)) {
        digits ++;
    }
    mut char mut[] upper = ['2' for i in 0 .. digits].dup,
        lower = ['=' for i in 0 .. digits].dup;
    for (digit in 0 .. digits) {
        bool test(char ch) {
            lower[digit] = ch;
            upper[digit] = ch;
            return l >= lower.decodeSnafu && l <= upper.decodeSnafu;
        }
        bottom die() assert(false);
        test('=') || test('-') || test('0') || test('1') || test('2') || die;
    }
    auto lower = lower.freeze, upper = upper.freeze;
    assert(lower == upper);
    return lower;
}
