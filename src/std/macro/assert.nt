module std.macro.assert;

macro import std.macro.quasiquoting;

import package(compiler).neat.base;

class ASTAssertion : ASTSymbol
{
    ASTSymbol test;

    string exprText;

    this(this.test, this.exprText, this.locRange) { }

    (Expression | Error) symbolTest(Context context, ASTSymbol sym) {
        auto context = context.withNamespace(context.compiler.exprAlias(
                context.namespace, "identName", context.compiler.stringLiteral(
                    context.platform, .repr(sym, context))));

        return (context.compiler.$expr "$identName = $(cast(int) $sym)")
            .compile(context)?.instanceOf(Expression)? else die;
    }

    (nullable Expression | Error) binopTest(mut Context context, ASTSymbol left, string op, ASTSymbol right)
    with (ErrorLoc(context.warmContext, locRange))
    {
        void set(string name, Symbol value) {
            context = context.withNamespace(
                context.compiler.exprAlias(context.namespace, name, value));
        }
        (ASTSymbol left, string op, ASTSymbol right) binop = (left, op, right);
        string leftStr = .repr(binop.left, context), rightStr = .repr(binop.right, context);
        set("leftStr", context.compiler.stringLiteral(context.platform, leftStr));
        set("rightStr", context.compiler.stringLiteral(context.platform, rightStr));
        auto
            left = binop.left.compile(context)?.instanceOf(Expression),
            right = binop.right.compile(context)?.instanceOf(Expression);
        if (!left || !right) return null;
        set("left", left);
        set("right", right);

        if (binop.op == "&&") {
            auto leftRecurse = dispatch(context, binop.left)?;
            auto rightRecurse = dispatch(context, binop.right)?;
            if (!leftRecurse || !rightRecurse) return null;
            set("leftRecurse", leftRecurse);
            set("rightRecurse", rightRecurse);

            return (context.compiler.$expr ({
                mut string ret;
                if (!left) ret = leftStr ~ " failed, because " ~ leftRecurse;
                else ret = rightStr ~ " failed, because " ~ rightRecurse;
                ret;
            })).compile(context)?.instanceOf(Expression);
        }
        if (binop.op == "||") {
            auto leftRecurse = dispatch(context, binop.left)?;
            auto rightRecurse = dispatch(context, binop.right)?;
            if (!leftRecurse || !rightRecurse) return null;
            set("leftRecurse", leftRecurse);
            set("rightRecurse", rightRecurse);

            return (context.compiler.$expr ({
                mut string ret;
                if (left) ret = rightStr ~ " failed, because " ~ rightRecurse;
                else if (right) ret = leftStr ~ " failed, because " ~ leftRecurse;
                else ret = leftStr ~ " failed (because " ~ leftRecurse ~ ") and "
                    ~ rightStr ~ " failed (because " ~ rightRecurse ~ ")";
                ret;
            })).compile(context)?.instanceOf(Expression);
        }
        bool isString(Type type) {
            auto elementType = context.compiler.isArrayType(type)? else return false;
            return context.compiler.isBasicType(elementType)? == "char" else false;
        }
        bool isFloat(Type type) {
            return context.compiler.isBasicType(type)? == "float" else false;
        }
        bool isLongCastable(Type type) {
            string type = context.compiler.isBasicType(type)? else return false;
            return type == "int" || type == "long" || type == "char" || type == "short";
        }
        if (left.type.isLongCastable && right.type.isLongCastable) {
            context.compiler.destructAstNumberLiteral(binop.right).case {
                long value:
                    return (context.compiler.$expr ({
                        import package(compiler).neat.runtime : ltoa;
                        leftStr ~ " = " ~ ltoa(cast(long) left);
                    })).compile(context)?.instanceOf(Expression);
                (:else): {}
            }
            return (context.compiler.$expr ({
                import package(compiler).neat.runtime : ltoa;
                leftStr ~ " = " ~ ltoa(cast(long) left)
                ~ " and " ~ rightStr ~ " = " ~ ltoa(cast(long) right);
            })).compile(context)?.instanceOf(Expression);
        }
        if (left.type.isFloat && right.type.isFloat) {
            return (context.compiler.$expr ({
                import package(compiler).neat.runtime : ftoa;
                leftStr ~ " = " ~ ftoa(left)
                ~ " and " ~ rightStr ~ " = " ~ ftoa(right);
            })).compile(context)?.instanceOf(Expression);
        }
        if (left.type.isString && right.type.isString) {
            context.compiler.destructAstStringLiteral(binop.right).case {
                string text:
                    return (context.compiler.$expr "$leftStr = \"$left\"")
                        .compile(context)?.instanceOf(Expression);
                (:else): {}
            }
        }
        return (context.compiler.$expr "$leftStr = \"$left\" and $rightStr = \"$right\"")
            .compile(context)?.instanceOf(Expression);
    }

    (nullable Expression | Error) negTest(mut Context context, ASTSymbol next)
    {
        void set(string name, Symbol value) {
            context = context.withNamespace(
                context.compiler.exprAlias(context.namespace, name, value));
        }

        auto nextRecurse = dispatch(context, next)?;
        if (!nextRecurse) return null;
        set("nextRecurse", nextRecurse);
        string nextStr = .repr(next, context);
        set("nextStr", context.compiler.stringLiteral(context.platform, nextStr));

        return (
            context.compiler.$expr nextRecurse ~ " was true"
        ).compile(context)?.instanceOf(Expression);
    }

    (nullable Expression | Error) dispatch(Context context, ASTSymbol sym) {
        context.compiler.destructAstIdentifier(sym).case {
            string ident: return symbolTest(context, sym);
            (:else): {}
        }
        context.compiler.destructAstMember(sym).case {
            (ASTSymbol base, ASTIdentifierSymbol member): return symbolTest(context, sym);
            (:else): {}
        }
        context.compiler.destructAstBinaryOp(sym).case {
            (ASTSymbol left, string op, ASTSymbol right):
                return binopTest(context, left, op, right);
            (:else): {}
        }
        context.compiler.destructAstNegation(sym).case {
            ASTSymbol next:
                return negTest(context, next);
            (:else): {}
        }
        return null;
    }

    override (Symbol | Error) compile(Context context) {
        mut Context context = context;
        mut ASTSymbol returnType = context.compiler.$type void;
        if (context.compiler.destructAstIdentifier(test)? == "false") {
            // special case: assert(false) is type bottom
            returnType = context.compiler.$type bottom;
        }

        if (auto ifFailed = dispatch(context, test)?) {
            string failedMsg = locRange.repr(context.compiler.fileIdTable) ~
                ": assertion failed: " ~ exprText ~ ", because ";
            context = context.withNamespace(context.compiler.exprAlias(
                    context.namespace, "ifFailed", ifFailed));
            context = context.withNamespace(context.compiler.exprAlias(
                    context.namespace,
                    "failedMsg",
                    context.compiler.stringLiteral(context.platform, failedMsg)));
            auto assertTest = context.compiler.$expr ({
                import neat.runtime : assert, print;
                if (!$test) {
                    print(failedMsg ~ ifFailed);
                    assert(false);
                }
                $returnType ret;
                ret;
            });
            return assertTest.compile(context);
        }
        string failedMsg = locRange.repr(context.compiler.fileIdTable) ~
            ": assertion failed: '" ~ exprText ~ "' was false";
        context = context.withNamespace(context.compiler.exprAlias(
                context.namespace,
                "failedMsg",
                context.compiler.stringLiteral(context.platform, failedMsg)));
        auto assertTest = context.compiler.$expr ({
            import neat.runtime : assert, print;
            if (!$test) {
                print(failedMsg);
                assert(false);
            }
            $returnType ret;
            ret;
        });
        return assertTest.compile(context);
    }
}

struct ErrorLoc {
    WarmContext __warmContext;
    LocRange __locRange;
    void onEnter() {
        __warmContext.errorStack ~= __locRange;
    }
    void onExit() {
        __warmContext.errorStack = __warmContext.errorStack[0 .. $ - 1];
    }
}

string repr(ASTSymbol sym, Context context) {
    import neat.runtime : assert;
    assert(!!sym);
    context.compiler.destructAstIdentifier(sym).case {
        string ident: return ident;
        (:else): {}
    }
    context.compiler.destructAstMember(sym).case {
        (ASTSymbol base, ASTIdentifierSymbol member):
            return repr(base, context) ~ "." ~ member.name(context.warmContext);
        (:else): {}
    }
    context.compiler.destructAstBinaryOp(sym).case {
        (ASTSymbol left, string op, ASTSymbol right):
            return "(" ~ repr(left, context) ~ " $op " ~ repr(right, context) ~ ")";
        (:else): {}
    }
    context.compiler.destructAstTemplateInstance(sym).case {
        (ASTSymbol decl, ASTSymbol[] types):
            mut string paramList;
            for (i, type in types) {
                if (i) paramList ~= ", ";
                paramList ~= repr(type, context);
            }
            if (types.length == 1)
                return repr(decl, context) ~ "!$paramList";
            return repr(decl, context) ~ "!($paramList)";
        (:else): {}
    }
    context.compiler.destructAstCast(sym).case {
        (ASTSymbol target, ASTSymbol value):
            return "cast(" ~ repr(target, context) ~ ") " ~ repr(value, context);
        (:else): {}
    }
    context.compiler.destructAstNegation(sym).case {
        ASTSymbol next: return "!" ~ repr(next, context);
        (:else): {}
    }
    context.compiler.destructAstNumberLiteral(sym).case {
        long value: return "$value";
        (:else): {}
    }
    context.compiler.destructAstStringLiteral(sym).case {
        string text: return "\"$text\"";
        (:else): {}
    }
    context.compiler.destructAstCall(sym).case {
        (ASTSymbol target, ASTArgument[] args): return repr(target, context) ~ "(" ~ reprArgs(args, context) ~ ")";
        (:else): {}
    }

    return "TODO";
}

string reprArgs(ASTArgument[] args, Context context) {
    mut string res;
    for (arg in args) {
        if (!res.empty) res ~= ", ";
        if (arg.name) res ~= "$(arg.name)=";
        res ~= repr(arg.sym, context);
    }
    return res;
}

class ParseAssert : Macro
{
    this() { }
    override void apply(MacroArgs args) {
        if (auto args = args.instanceOf(ParseExpressionBaseArgs)) {
            args.symbol = this.parse(args.parser, args.lexicalContext);
        }
    }

    (nullable ASTSymbol | Error) parse(Parser parser, LexicalContext context) {
        parser.begin;
        if (!parser.acceptIdentifier("assert")) {
            parser.revert;
            return null;
        }
        if (!parser.acceptToken(TokenType.lparen)) {
            parser.revert;
            return null;
        }
        parser.commit;
        auto exprStartFrom = parser.from;
        auto exprStart = parser.remainingText;
        auto expression = context.compiler.parseExpression(parser, context)?? else die;
        auto exprText = exprStart[0 .. $ - parser.remainingText.length];

        auto exprRange = parser.to(exprStartFrom);
        parser.expectToken(TokenType.rparen)?;

        return new ASTAssertion(expression, exprText, exprRange);
    }
}

void assertMacro(MacroState macroState)
{
    macroState.addMacro(new ParseAssert);
}

macro(assertMacro);
