module std.macro.cimport;

// IMPORTANT
// Do not import neat.stuff here!
// That package takes like a second to compile!
// Extend CompilerBase instead!
import package(compiler).neat.base;
import package(compiler).neat.decl;
import package(compiler).neat.expr;
import package(compiler).neat.parser;
import package(compiler).neat.struct_;
import package(compiler).neat.types;
import package(compiler).neat.util;
import package(compiler).helpers;
import package(compiler).neat.union_;

extern(C) void neat_runtime_system(string command);

class CImportMacro : Macro
{
    this() { }

    override void apply(MacroArgs args) {
        auto args = args.instanceOf(ParseImportArgs);
        if (args) {
            args.module_ = this.parse(args.parser, args.lexicalContext);
        }
    }

    (nullable ASTModuleBase | fail Error) parse(Parser parser, LexicalContext lexicalContext) {
        auto compiler = lexicalContext.compiler;
        if (!parser.acceptIdentifier("c_header")?)
            return null;
        parser.expect("(")?;
        // TODO move into neat.base
        auto from = parser.loc;
        parser.expect("\"")?;
        auto includeSym = compiler.parseStringLiteral(parser, lexicalContext, "\"", from)?.instanceOf(ASTStringLiteral);
        parser.assert_(!!includeSym, "string expected")?;
        mut string flags;
        if (parser.accept(",")?) {
            auto from = parser.loc;
            parser.expect("\"")?;
            auto flagsSym = compiler.parseStringLiteral(parser, lexicalContext, "\"", from)?.instanceOf(ASTStringLiteral);
            parser.assert_(!!flagsSym, "flags string expected")?;
            flags = flagsSym.text ~ " ";
        }
        parser.expect(")")?;
        if (compiler.cFlags.length > 0)
            flags = (compiler.cFlags ~ flags).join(" ");

        return parseIncludeModule(includeSym.text, flags, lexicalContext);
    }

    (nullable ASTModuleBase | fail Error) parseIncludeModule(
        string include, string flags, LexicalContext lexicalContext)
    {
        import std.string : replace;

        auto compiler = lexicalContext.compiler;
        string modName = include.replace("/", "_").replace(".", "_").replace("-", "_");

        auto headerBase = "#include <" ~ include ~ ">\n";
        auto headerName = ".obj/cimport." ~ modName ~ ".h";

        // Assume system headers don't change.
        // TODO: Is this legitimate? Can we timestamp the includes?
        if (!exists(headerName ~ ".out")) {
            // TODO randomize
            headerName.write(headerBase);
            string cmd = "gcc " ~ flags ~ "-dD -E " ~ headerName ~ " > " ~ headerName ~ ".out";
            print("\$ " ~ cmd);
            neat_runtime_system(cmd);
        }
        string csource = read(headerName ~ ".out");
        // do not count references on the file source - it will end
        // up with lots of small inc/decs to fragments that we never free anyway
        (cast(size_t*) &csource)[2] = 0;
        auto cparser = lexicalContext.compiler.createParser(headerName ~ ".out", csource);

        auto cparserHelper = new CParserHelper(cparser);

        auto pak = new Package("c_header", "", []);
        cparserHelper.mod = compiler.astModule(modName, pak, new MacroState, cparser.loc, null);

        void add(string name, (ASTExternFunctionBase | ASTSymbol) symbol) {
            if (false)
                symbol.case {
                    ASTExternFunctionBase ex:
                        print("define " ~ ex.name ~ ": " ~ ex.repr);
                    ASTSymbol sym:
                        if (!sym.instanceOf(ASTIntLiteral)) {
                            print("define " ~ name ~ " = " ~ sym.repr);
                        }
                }
            symbol.case {
                ASTExternFunctionBase a:
                    cparserHelper.mod.addEntry(Protection.public_, a);
                ASTSymbol sym: {
                    if (!cparserHelper.mod.getSymbolAlias(name)) {
                        cparserHelper.mod.addAliasDecl(name, Protection.public_, sym);
                    }
                }
            }
        }

        (void | fail Error) eatAttribute() {
            while (cparser.accept("__attribute__")?) {
                cparser.accept("(")?; cparser.accept("(")?;
                mut int parenlevel = 2;
                while (parenlevel) {
                    auto open = cparser.text.find("("), close = cparser.text.find(")");
                    assert(close != -1);
                    if (open != -1 && open < close) {
                        cparser.drop(open + 1);
                        parenlevel += 1;
                    } else {
                        cparser.drop(close + 1);
                        parenlevel -= 1;
                    }
                }
            }
        }

        while (!cparser.eof?) {
            if (!cparserHelper.sysmod && (
                cparser.accept("# 1 \"<command-line>\"")?
                || cparser.accept("# 0 \"<command-line>\"")?)) {
                // start of actual include
                cparserHelper.sysmod = cparserHelper.mod;
                cparserHelper.mod = compiler.astModule(modName, pak, new MacroState, cparser.loc, null);
            }
            if (cparser.accept("#define ")?) {
                auto identifier = cparser.parseIdentifier?;
                if (cparser.accept("(")?) { cparser.eatline; continue; }
                auto start = cparser.loc;
                auto num = cparserHelper.parseCNumber?;
                if (cparser.accept(",")?) { // comma expression?!
                    cparser.eatline;
                    continue;
                }
                num.case {
                    (:failure): { cparser.eatline; continue; }
                    (:success, int i): add(identifier, new ASTIntLiteral(i, start));
                }
                cparser.assert_(cparser.text.startsWith("\n"), "unknown text")?;
                // cparser.expect("\n");
                continue;
            }
            if (cparser.accept("typedef")?) {
                auto type = cparserHelper.parseType?;
                if (!type) { cparser.eatline; continue; }
                auto type = notNull!ASTSymbol(type);
                auto decl = cparserHelper.parseTypedDecl(type)?;
                if (!decl.type) { cparser.eatline; continue; }
                eatAttribute?;
                if (!cparser.accept(";")?) { cparser.eatline; continue; }
                add(decl.name, notNull!ASTSymbol(decl.type));
                continue;
            }
            // match struct foo { }; as "foo"
            if (cparser.accept("struct")?) {
                auto pair = cparserHelper.parseStructType?;
                if (!pair.type) { cparser.eatline; continue; }
                add(pair.name, pair.type);
                continue;
            }
            if (cparser.accept("union")?) {
                auto pair = cparserHelper.parseStructType(union_=true)?;
                if (!pair.type) { cparser.eatline; continue; }
                add(pair.name, pair.type);
                continue;
            }
            if (cparser.accept("enum")?) {
                auto type = cparserHelper.parseEnumType?;
                if (!type) { cparser.eatline; continue; }
                continue;
            }
            if (cparser.accept("#")?) {
                cparser.eatline;
                continue;
            }
            cparser.accept("extern")?;
            eatAttribute?;
            cparser.accept("extern")?;
            {
                (bool | fail Error) closeFunction() {
                    if (!cparser.accept(")")?) return false;
                    eatAttribute?;
                    return cparser.accept(";")?;
                }
                auto start = cparser.loc;
                cparser.begin;
                if (auto retBase = cparserHelper.parseType?) {
                    auto retDecl = cparserHelper.parseTypedDecl(retBase)?;
                    if (retDecl.type && retDecl.name.length > 0 && cparser.accept("(")?) {
                        mut ASTParameter[] params;
                        mut bool success;
                        while (true) {
                            if (closeFunction?) {
                                cparser.commit;
                                if (params.length == 1) {
                                    auto first = params[0].type.instanceOf(ASTIdentifier);
                                    if (first && first.name == "void") {
                                        params = [];
                                    }
                                }
                                add(retDecl.name, compiler.astExternFunction(
                                    retDecl.name, notNull!ASTSymbol(retDecl.type), params, variadic=false, start));
                                success = true;
                                break;
                            }
                            if (params.length && !cparser.accept(",")?) break;
                            auto argloc = cparser.loc;
                            if (auto argBasetype = cparserHelper.parseType?) {
                                auto argDecl = cparserHelper.parseTypedDecl(argBasetype)?;
                                if (!argDecl.type) break;
                                params ~= ASTParameter(
                                    argDecl.name, false,
                                    type=notNull!ASTSymbol(argDecl.type),
                                    defaultValue=null,
                                    argloc);
                            } else break;
                        }
                        if (success) continue;
                        // print("fail " ~ retDecl.name ~ ": " ~ cparser.text[0 .. 100]);
                    }
                }
                // cparser.assert_(false, "here 2?")?;
                cparser.revert;
            }
            // cparser.assert_(false, "here?")?;
            cparser.eatline;
        }

        return cparserHelper.mod;
    }
}

void eatline(Parser parser) {
    auto pos = parser.text.find("\n");
    if (pos == -1) parser.drop(cast(int) parser.text.length);
    else parser.drop(pos + 1);
}

class CLong : ASTSymbol
{
    this() { }

    override (Type | fail Error) compile(Context context) {
        // FIXME this does not apply on win32
        return context.nativeWordType;
    }

    override ASTSymbol quote(Quoter quoter) { print("cannot quote 'CLong'!"); assert(false); }
    override string repr() { return "long int(C)"; }
}

class CStruct : Struct
{
    override bool same(Type other) {
        if (auto otherStruct = other.instanceOf(CStruct)) {
            return this.name == otherStruct.name;
        }
        return false;
    }
}

class CStaticArray : ASTSymbol
{
    ASTSymbol type;

    int count;

    this(this.type, this.count, this.locRange) { }

    override (Type | fail Error) compile(Context context) {
        import package(compiler).neat.struct_ : Struct;

        // TODO give this a better type
        mut StructMemberDeclaration[] members;
        for (i in 0 .. count)
            members ~= new StructMemberDeclaration(
                "_" ~ ltoa(i), Protection.public_, type.compile(context)?.beType(this.locRange)?);

        return new Struct("SA_hack", members, this.locRange, context.namespace.mangle);
    }

    override ASTSymbol quote(Quoter quoter) { print("cannot quote 'CStaticArray'!"); assert(false); }
    override string repr() { return type.repr ~ "[" ~ itoa(count) ~ "](C)"; }
}

class CParserHelper {
    Parser parser;

    ASTModuleBase mod, sysmod;

    this(this.parser) { }

    (nullable ASTSymbol | fail Error) parseType() {
        parser.acceptIdentifier("const")?;
        if (auto ret = parseBasicCType?) {
            return ret;
        }
        auto ident = parser.parseIdentifier?;
        if (ident == "struct") {
            auto ret = parseStructType?;
            return ret.type;
        }
        if (ident == "union") {
            auto ret = parseStructType(union_=true)?;
            return ret.type;
        }
        if (ident == "enum") {
            return parseEnumType?;
        }
        if (ident.length > 0) {
            if (auto definedSym = this.mod.getSymbolAlias(ident)) return definedSym;
            if (auto definedSym = this.sysmod.getSymbolAlias(ident)) return definedSym;
            // print("No such ident " ~ ident);
        }
        return null;
    }

    (nullable ASTSymbol type, string name | fail Error) parseTypedDecl(ASTSymbol baseType) {
        mut auto current = baseType;
        assert(!!current);
        auto start = parser.loc;
        while (true) {
            if (parser.accept("*")? || parser.accept("[]")?) {
                current = new ASTPointer(current, parser.loc);
                parser.accept("const")?;
                parser.accept("restrict")?;
                parser.accept("__restrict")?;
                continue;
            }
            // function pointer

            parser.begin;
            if (parser.accept("(")? && parser.accept("*")?) {
                auto name = parser.parseIdentifier?;
                // I fucking hate C type syntax. No arrays of function pointers for you.
                if (!parser.accept(")")?) {
                    parser.revert;
                    return (null, "");
                }
                if (!parser.accept("(")?) {
                    parser.revert;
                    return (null, "");
                }
                mut ASTSymbol[] params;
                while (!parser.accept(")")?) {
                    if (params.length) parser.expect(",")?;
                    auto argtype = parseType?;
                    if (!argtype) {
                        parser.revert;
                        return (null, "");
                    }
                    auto arg = parseTypedDecl(notNull!ASTSymbol(argtype))?;
                    if (!arg.type) {
                        parser.revert;
                        return (null, "");
                    }
                    params ~= notNull!ASTSymbol(arg.type);
                }
                if (params.length == 1) {
                    if (auto identifier = params[0].instanceOf(ASTIdentifier)) {
                        if (identifier.name == "void") {
                            params = [];
                        }
                    }
                }
                parser.commit;
                return (new ASTFunctionPointer(current, params, start), name);
            }
            parser.revert;
            break;
        }
        // name can be empty!
        with (parser.transaction) {
            auto name = parser.parseIdentifier?;
            auto start = parser.loc;
            while (parser.accept("[")?) {
                if (parser.accept("]")?) {
                    current = new ASTPointer(current, parser.loc);
                    continue;
                }
                mut int num;
                auto cn = parseCNumber?;
                // TODO if ((:success, int i) <- parseCNumber?)
                if (cn.case(:failure: false, (:success, int): true)) {
                    num = cn.case(:failure: 0, (:success, int i): i);
                } else if (
                    parser.accept("sizeof(void *) <= 8 ? 56 : sizeof(void *) == 16 ? 64 : 3 * sizeof(void *)")?) {
                    // Jesus Christ, SDL, what the fuck.
                    // TODO full C expression parser, apparently
                    num = 64;
                } else {
                    return (null, "");
                }
                // parser.expect("]")?;
                if (!parser.accept("]")?) return (null, "");
                current = new CStaticArray(current, num, start);
            }
            commit;
            return (current, name);
        }
    }

    (nullable ASTSymbol | fail Error) parseBasicCType() {
        with (parser) {
            begin;
            auto type = parser.parseIdentifier?;
            if (type == "void") { commit; return .type("void"); }
            if (type == "float") { commit; return .type("float"); }
            if (type == "double") { commit; return .type("double"); }
            if (type == "_Bool") { commit; return .type("bool"); }
            revert;
            begin;
            mut bool signed, unsigned, long_, longlong, short_, char_, int_;
            while (true) {
                begin;
                auto part = parser.parseIdentifier?;
                if (part == "signed") { commit; signed = true; continue; }
                if (part == "unsigned") { commit; unsigned = true; continue; }
                if (part == "long") {
                    commit;
                    long_ = true;
                    if (parser.acceptIdentifier("long")?) longlong = true;
                    continue;
                }
                if (part == "short") { commit; short_ = true; continue; }
                if (part == "char") { commit; char_ = true; continue; }
                if (part == "int") { commit; int_ = true; continue; }
                revert;
                break;
            }
            if (longlong) { commit; return .type("long"); }
            if (long_) { commit; return new CLong; }
            if (short_) { commit; return .type("short"); }
            if (char_) { commit; return .type("char"); }
            if (signed || unsigned || int_) { commit; return .type("int"); }
            revert;
            return null;
        }
    }

    (ASTSymbol type, string name | fail Error) parseStructType(bool union_=false) {
        auto start = parser.loc;
        auto name = parser.parseIdentifier?;
        mut ASTStructMember[] members;
        auto emptyStruct = new ASTStructDeclSym(new ASTStructDecl(name, [], start)); // opaque
        if (!parser.accept("{")?) {
            return (new ASTFallbackIdentifier(name, emptyStruct), name);
        }
        void scanToClosingBracket() {
            mut int bracketDepth = 1;
            // TODO factor out with () skipping
            while (bracketDepth) {
                auto open = parser.text.find("{"), close = parser.text.find("}");
                assert(close != -1);
                if (open != -1 && open < close) {
                    parser.drop(open + 1);
                    bracketDepth += 1;
                } else {
                    parser.drop(close + 1);
                    bracketDepth -= 1;
                }
            }
        }
        while (!parser.accept("}")?) {
            parser.begin;
            // #defines inside structs happen occasionally
            if (parser.accept("#define ")?) { parser.eatline; parser.commit; continue; }
            // line number annotation
            if (parser.accept("# ")?) { parser.eatline; parser.commit; continue; }
            auto baseType = parseType?;
            if (!baseType) { parser.revert; scanToClosingBracket; return (emptyStruct, name); }
            auto baseType = notNull!ASTSymbol(baseType);
            mut auto memberDecl = parseTypedDecl(baseType)?;
            void flush() {
                members ~= (memberDecl.name, Protection.public_, notNull!ASTSymbol(memberDecl.type));
            }
            while (parser.accept(",")?) {
                if (!memberDecl.type) { parser.revert; scanToClosingBracket; return (emptyStruct, name); }
                flush;
                memberDecl = parseTypedDecl(baseType)?;
            }
            if (!memberDecl.type) { parser.revert; scanToClosingBracket; return (emptyStruct, name); }
            flush;
            if (parser.accept(":")?) {
                // It's hopeless. Just try, I guess.
                parser.parseNumber?;
            }
            // parser.expect(";")?;
            if (!parser.accept(";")?) { parser.revert; scanToClosingBracket; return (emptyStruct, name); }
            parser.commit;
        }
        if (union_) {
            mut ASTUnionMember[] unionMembers;
            for (a in members) unionMembers ~= (a.name, a.type);
            return (new ASTUnion(name, unionMembers, null, start), name);
        }
        mut ASTStructEntry[] entries;
        for (a in members) entries ~= a;
        return (new ASTStructDeclSym(new ASTStructDecl(name, entries, start)), name);
    }

    (nullable ASTSymbol | fail Error) parseEnumType() {
        // TODO export enum members as aliases
        auto start = parser.loc;
        parser.parseIdentifier?;
        if (!parser.accept("{")?) return null; // wat
        mut int lastNum = -1;
        while (true) {
            auto name = parser.parseIdentifier?;
            if (!name.length) break;
            if (parser.accept("=")?) {
                auto num = parseCNumber?;
                num.case {
                    (:failure): break;
                    (:success, int i): {
                        mod.addAliasDecl(name, Protection.public_, new ASTIntLiteral(i, start));
                        lastNum = i;
                    }
                }
            } else {
                mod.addAliasDecl(name, Protection.public_, new ASTIntLiteral(lastNum + 1, start));
                lastNum++;
            }
            parser.accept(",")?;
        }
        auto close = parser.text.find("}");
        parser.drop(close + 1);
        return type("int");
    }

    (:failure | :success, int | fail Error) parseCNumber() {
        // TODO fix refcount again
        // with (parser) {
            parser.begin;
            mut int num;
            (bool | fail Error) accept(string s) { return parser.accept(s)?; }
            parser.parseNumber?.case {
                (:failure): { parser.revert; return :failure; }
                (:success, int i): num = i;
            }
            // weird C shit
            if (accept("e")? || accept("E")? || accept(".")?) {
                parser.revert;
                return :failure;
            }
            accept("U")?; accept("u")?;
            accept("L")?; accept("l")?;
            accept("L")?; accept("l")?;
            parser.commit;
            return (:success, num);
        // }
    }
}

private ASTSymbol type(string ident) {
    return new ASTIdentifier(ident, false, __RANGE__);
}

// the reason for this is we see `typedef struct Foo_ Foo` before we see `struct Foo_` -
// but sometimes we don't see `struct Foo_` at all; in that case we treat it as `struct {}`.
class ASTFallbackIdentifier : ASTSymbol
{
    string name;
    ASTStructDeclSym emptyStruct;
    this(this.name, this.emptyStruct) {}
    override (Symbol | fail Error) compile(Context context) {
        if (auto res = context.namespace.lookup(this.name, context, LookupReason.identifier, __RANGE__)?) {
            if (res.instanceOf(Type)) return res;
        }
        return this.emptyStruct.compile(context);
    }
}

class ASTStructDeclSym : ASTSymbol
{
    ASTStructDecl decl;

    this(this.decl) { }

    override (Symbol | fail Error) compile(Context context) {
        auto struct_ = this.decl.compileSymbol(context)?;
        auto cstruct = new CStruct(struct_.name, struct_.members, struct_.locRange, context.namespace.mangle);

        return cstruct;
    }
}

void addCImportMacro(MacroState macroState)
{
    macroState.addMacro(new CImportMacro);
}

macro(addCImportMacro);
