module backend.c;

import backend.base;
import helpers;
import polyhash;
import package(compiler).std.algorithm;
macro import package(compiler).std.macro.listcomprehension;

class CBackend : Backend {
    this() { }
    override CBackendModule createModule(
        Platform platform, string targetFile, BackendLocation loc, bool forMacro)
    {
        return new CBackendModule(platform, forMacro);
    }
    override void hash(Hash hash) {
        hash.adds("gcc");
    }
    override void linkSharedLib(string soname, string flags, bool verbose, string[] objFiles, Platform platform) {
        string objStr = [join " " ~ a for a in objFiles];
        mut string basecmd = getEnv("CC", "gcc") ~ " -fpic -fno-strict-aliasing "
            ~ flags;
        if (platform.wordSize == 8) {
            basecmd ~= " -m64";
        } else {
            basecmd ~= " -m32";
        }
        string cmd = "$basecmd -shared -g $objStr -o $soname";
        if (verbose) {
            print("\$ $cmd");
        }
        neat_runtime_system(cmd);
    }
}

private string getEnv(string key, string default_ = "") {
    import package(compiler).std.process : getEnv;
    auto ret = getEnv(key);
    if (!ret.empty) return ret;
    return default_;
}

struct Global {
    string name;
    nullable BackendType type;
}

struct Globals {
    Global[string] globals;
    void add(Global global) {
        this.globals[global.name] = global;
    }
    bool defined(string name) {
        return this.globals.has(name);
    }
    BackendType type(string name) {
        if (!defined(name)) {
            print("cannot get type of unknown global $name");
            assert(false);
        }
        return this.globals[name].type? else die;
    }
}

extern(C) void neat_runtime_file_write(string file, string content);
extern(C) void neat_runtime_system(string command);
extern(C) void neat_runtime_dlcall(string soname, string name, void* arg);
extern(C) bool neat_runtime_symbol_defined_in_main(string symbol);

class CBackendModule : BackendModule {
    LineWriter output;
    Platform platform;

    mut int literalId;
    Globals globals;

    bool forMacro;

    this(Platform platform, this.forMacro) {
        this.output = new LineWriter;
        this.platform = platform;
        this.output.print("#include <stdlib.h>");
        this.output.print("#include <string.h>");
        this.output.print("typedef struct { } VOIDTYPE;");
        defineCIntrinsics(this, platform);
    }

    string typeFmt(BackendType type, bool indirect=false) {
        return .typeFmt(this, type, indirect=indirect);
    }

    override bool declared(string name) {
        return this.globals.defined(name);
    }

    override void declare(string name, BackendType ret, BackendType[] args, bool variadic)
    {
        if (this.declared(name)) return;
        auto fpType = new BackendFunctionPointerType(ret, args, variadic, this.platform);
        this.globals.add(Global(name, fpType));
        if (name == "memcpy" || name == "malloc" || name == "calloc"
            || name == "strlen" || name == "realpath" || name == "getenv"
            || name == "atoi" || name == "atof" || name == "atoll" || name == "memcmp"
            || name == "memset" || name == "free" || name == "exit"
        ) return; // in header, don't redeclare - may conflict
        mut string qualifier = "";
        if (this.platform.win64) {
            if (forMacro) {
                if (neat_runtime_symbol_defined_in_main(name)) {
                    qualifier ~= "__declspec(dllimport) ";
                } else {
                    qualifier ~= "__declspec(dllexport) ";
                }
            } else {
                qualifier ~= "__declspec(dllexport) ";
            }
        }
        mut string argstr = "(";
        mut int argnum = 0;
        void add(string arg) {
            if (argnum) argstr ~= ", ";
            argstr ~= arg ~ " arg$(argnum++)";
        }
        void recurse(BackendType arg) {
            if (arg.size == 0) return;
            if (auto struct_ = arg.instanceOf(BackendStructType)) {
                if (struct_.internal && struct_.size > 16) {
                    [recurse(member) for member in struct_.members];
                    return;
                }
            }
            add(typeFmt(arg));
        }
        [recurse(arg) for arg in args];
        if (variadic) {
            if (argnum) argstr ~= ", ";
            argstr ~= "...";
        }
        argstr ~= ")";
        mut string retstr;
        if (ret.instanceOf(BackendVoidType)) {
            retstr = "void";
        } else {
            retstr = typeFmt(ret);
        }
        this.output.print(qualifier ~ " " ~ retstr ~ " " ~ name ~ argstr ~ ";");
    }

    void defineVar(string name, BackendType type, bool threadlocal)
    {
        if (this.declared(name)) return;
        string modifier = "__thread " if threadlocal else "";
        this.output.print(modifier ~ typeFmt(type) ~ " $name;");
        this.globals.add(Global(name, type));
    }

    void declareVar(string name, BackendType type, bool threadlocal)
    {
        if (this.declared(name)) return;
        string modifier = "__thread " if threadlocal else "";
        this.output.print("extern $modifier" ~ typeFmt(type) ~ " $name;");
        this.globals.add(Global(name, type));
    }

    override BackendFunction define(
        string name, string decoration, BackendType ret, BackendType[] args, BackendLocation loc)
    {
        auto fpType = new BackendFunctionPointerType(ret, args, variadic=false, this.platform);
        this.globals.add(Global(name, fpType));
        return new CBackendFunction(this, name, ret, args);
    }
    override void declareSymbolList(string name, size_t length) {
        string globalDecl = "void *" ~ name ~ "[" ~ ltoa(length) ~ "]";
        mut string modifier;
        if (this.platform.win64 && forMacro && neat_runtime_symbol_defined_in_main(name)) {
            modifier ~= "__declspec(dllimport) ";
        }
        this.output.print("extern $modifier" ~ globalDecl ~ ";");
        this.globals.add(Global(name, null));
    }
    override void defineSymbolList(string name, (string | size_t)[] symbols)
    {
        if (forMacro && neat_runtime_symbol_defined_in_main(name))
            return;

        mut string symbolList;
        for (i, symbol in symbols) {
            if (i) symbolList ~= ", ";
            symbolList ~= "(void*) " ~ symbol.case(string s: s, size_t l: ltoa(l));
        }
        string globalDecl = "void *" ~ name ~ "[" ~ ltoa(symbols.length) ~ "]";
        this.output.print(globalDecl ~ " = { " ~ symbolList ~ "};");
        this.globals.add(Global(name, null));
    }
    string nextLiteral() {
        string res = "@str" ~ itoa(this.literalId);
        this.literalId = this.literalId + 1;
        return res;
    }
}

string typeMangle(BackendType type) {
    if (type.instanceOf(BackendLongType)) return "l";
    if (type.instanceOf(BackendIntType)) return "i";
    if (type.instanceOf(BackendShortType)) return "s";
    if (type.instanceOf(BackendCharType)) return "c";
    if (type.instanceOf(BackendFloatType)) return "f";
    if (type.instanceOf(BackendDoubleType)) return "d";
    if (type.instanceOf(BackendVoidType)) return "v";
    if (auto backendSpacerType = type.instanceOf(BackendSpacerType)) {
        return "U" ~ ltoa(backendSpacerType.size) ~ "a" ~ ltoa(backendSpacerType.alignment);
    }
    if (auto backendPointerType = type.instanceOf(BackendPointerType)) {
        return "P" ~ typeMangle(backendPointerType.target);
    }
    if (auto backendStructType = type.instanceOf(BackendStructType)) {
        mut string mangle = "S" ~ ltoa(backendStructType.members.length);
        for (i, member in backendStructType.members) {
            mangle ~= typeMangle(member);
        }
        return mangle;
    }
    if (auto backendFpType = type.instanceOf(BackendFunctionPointerType)) {
        mut string res;
        if (backendFpType.variadic) res ~= "Fv";
        else res ~= "F";
        res ~= typeMangle(backendFpType.ret);
        res ~= ltoa(backendFpType.params.length);
        for (i, param in backendFpType.params) {
            res ~= typeMangle(param);
        }
        return res;
    }
    if (auto backendSAtype = type.instanceOf(BackendStaticArrayType)) {
        return "A" ~ typeMangle(backendSAtype.element) ~ "l" ~ itoa(backendSAtype.length);
    }
    assert(false);
}

string typeFmt(CBackendModule mod, BackendType type, bool indirect) {
    if (type.instanceOf(BackendLongType)) return "long long int";
    if (type.instanceOf(BackendIntType)) return "int";
    if (type.instanceOf(BackendShortType)) return "short";
    if (type.instanceOf(BackendCharType)) return "char";
    if (type.instanceOf(BackendFloatType)) return "float";
    if (type.instanceOf(BackendDoubleType)) return "double";
    if (type.instanceOf(BackendVoidType)) {
        if (indirect) return "void";
        else assert(false);
    }
    if (auto backendSpacerType = type.instanceOf(BackendSpacerType)) {
        // Naked arrays are awkward in C, so make sure it's bundled in a struct.
        string mangle = typeMangle(type);
        string typestr = "arr_" ~ mangle;
        if (mod.declared(typestr)) return typestr;
        string def = "typedef struct { char value[" ~ ltoa(backendSpacerType.size) ~ "]; } " ~ typestr ~ " " ~
            "__attribute__ ((aligned (" ~ itoa(backendSpacerType.alignment) ~ ")));";
        mod.output.print(def);
        mod.globals.add(Global(typestr, null));
        return typestr;
    }
    if (auto backendPointerType = type.instanceOf(BackendPointerType)) {
        return mod.typeFmt(backendPointerType.target, indirect=true) ~ "*";
    }
    if (auto backendStructType = type.instanceOf(BackendStructType)) {
        string mangle = typeMangle(type);
        string typestr = "str_" ~ mangle;
        if (mod.declared(typestr)) return typestr;
        mut string def = "typedef struct { ";
        for (int i, member in backendStructType.members) {
            if (member.size > 0) {
                def ~= mod.typeFmt(member) ~ " _" ~ itoa(i) ~ ";";
            } else {
                string fieldmangle = typeMangle(member);
                def ~= "/* $fieldmangle _$(i); */";
            }
        }
        def ~= "} " ~ typestr ~ ";";
        mod.output.print(def);
        mod.globals.add(Global(typestr, null));
        return typestr;
    }
    if (auto backendFpType = type.instanceOf(BackendFunctionPointerType)) {
        string mangle = typeMangle(type);
        string typestr = "fp_" ~ mangle;
        if (mod.declared(typestr)) return typestr;
        mut string def = "typedef ";
        def ~= mod.typeFmt(backendFpType.ret, indirect=true) ~ "(*" ~ typestr ~ ")(";
        mut bool first = true;
        void add(string s) {
            if (first) first = false;
            else def ~= ", ";
            def ~= s;
        }
        void recurse(BackendType type) {
            if (type.size == 0) return;
            if (auto struct_ = type.instanceOf(BackendStructType)) {
                if (struct_.internal && struct_.size > 16) {
                    [recurse(member) for member in struct_.members];
                    return;
                }
            }
            add(mod.typeFmt(type, indirect=false));
        }
        [recurse(param) for param in backendFpType.params];
        if (backendFpType.variadic) {
            add("...");
        }
        def ~= ");";
        mod.output.print(def);
        mod.globals.add(Global(typestr, null));
        return typestr;
    }
    if (auto backendSAtype = type.instanceOf(BackendStaticArrayType)) {
        return mod.typeFmt(backendSAtype.element, indirect=true) ~ "[" ~ itoa(backendSAtype.length) ~ "]";
    }
    assert(false);
}

alias Instr = (
      :arg, int index
    | :shortLiteral, int value
    | :byteLiteral, int value
    | :intLiteral, long value
    | :longLiteral, long value
    | :wordLiteral, Platform platform, long value
    | :floatLiteral, float value
    | :doubleLiteral, double value
    | :stringLiteral, string text
    | :zeroLiteral, BackendType type
    | :unreachable
    | :structLiteral, BackendStructType struct_, int[] regs
    | :symbolList, string name
    | :globalVar, string name, BackendType type, bool define, bool threadlocal
    | :binOp, string op, BackendType type, int left, int right
    | :bitCast, int from, BackendType to
    | :zeroExtend, int value, BackendType to
    | :signExtend, int value, BackendType to
    | :trunc, int value, BackendType to
    | :convert, int from, BackendType to
    | :callInstr, BackendType retType, string name, int[] args
    | :getFuncPtr, BackendType type, string name
    | :callFuncPtr, BackendType type, int regId, int[] args
    | :load, BackendType type, int regId
    | :staticAlloca, BackendType type
    | :field, BackendType type, int regId, int index
    | :fieldOffset, BackendType type, int regId, size_t index
    | :ptrOffset, BackendType type, int ptrReg, int offsetReg
    | :store, BackendType type, int targetReg, int valueReg
    | :return_, int regId
    | :setLabel, string label
    | :branch, string label
    | :testBranch, int regId, string thenLabel, string elseLabel
    | :switch_, int regId, (int value, string label, bool unlikely)[] targets, string defaultLabel
    | :setLocation, BackendLocation loc
);

/**
 * SSA results can be held back for a future instruction to compose.
 * However, whenever a global state change point is reached,
 * such as a store, or function call, all held SSA
 * instructions must be flushed.
 */

// flow for 1 use:
// pending -> held -> done
// pending -> held -> persisted
// flow for multiple uses:
// pending -> persisted
enum SSAMode
{
    pending,
    // reg$reg
    persisted,
    // (int) 5
    held,
    done
}

struct Value {
    string type;
    string value;
    bool exactType;

    // empty type and empty value: void, never emitted
    bool fake() {
        return type == "" && value == "";
    }

    static Value none() {
        return Value(type="", value="", exactType=true);
    }

    string repr() {
        return "Value(type=$type, value=$value, exactType=$(cast(int) exactType))";
    }
}

struct SSAInstr {
    SSAMode mode;
    int reg; // -1 for terminators
    // inc for every instr that uses us.
    // one-use instrs can be inlined.
    int uses;
    Value heldValue;
    Instr instr;
}

class CBackendFunction : BackendFunction {
    CBackendModule mod;
    BackendType ret_;
    BackendType[] args;

    LineWriter output;
    mut int blockId;

    mut SSAInstr mut[] instrs;

    mut string[] labels;

    this(this.mod, this.name, this.ret_, this.args)
    {
        this.output = new LineWriter;
    }

    override string toString() => "CBackendFunction($(this.name))";

    void print(string msg) {
        this.output.append("  ");
        this.output.append(msg);
        this.output.endl;
    }
    override string getLabel() {
        return "Label" ~ itoa(this.blockId);
    }
    override string getCurrentLabel() {
        return this.labels[$ - 1];
    }
    override void setLabel(string label) {
        if (!labels.empty && labels[$ - 1] == label) return;
        if ([any a == label for a in labels]) {
            .print("duplicate label $label");
            assert(false);
        }
        labels ~= label;
        addInstr((:setLabel, label));
    }
    override void setLocation(BackendLocation loc) {
        addInstr((:setLocation, loc));
    }
    int addInstr(Instr instr) {
        int reg = cast(int) this.instrs.length;
        this.instrs ~= SSAInstr(SSAMode.pending, reg, 0, Value.none, instr);
        return reg;
    }
    void addBlockEnderInstr(Instr instr) {
        this.instrs ~= SSAInstr(SSAMode.pending, -1, 0, Value.none, instr);
    }
    void useReg(int reg) {
        this.instrs[reg].uses += 1;
    }
    override int arg(int index) {
        return addInstr((:arg, index));
    }
    override int intLiteral(long value) {
        return addInstr((:intLiteral, value));
    }
    override int shortLiteral(int value) {
        return addInstr((:shortLiteral, value));
    }
    override int byteLiteral(int value) {
        return addInstr((:byteLiteral, value));
    }
    override int longLiteral(long value) {
        return addInstr((:longLiteral, value));
    }
    override int floatLiteral(float value) {
        return addInstr((:floatLiteral, value));
    }
    override int doubleLiteral(double value) {
        return addInstr((:doubleLiteral, value));
    }
    override int wordLiteral(Platform platform, long value) {
        return addInstr((:wordLiteral, platform, value));
    }
    override int stringLiteral(string text) {
        return addInstr((:stringLiteral, text));
    }
    override int voidLiteral() {
        return zeroLiteral(new BackendVoidType);
    }
    override void unreachable() {
        addBlockEnderInstr(:unreachable);
        // TODO
        // this.blockId++;
        this.blockId += 1;
    }
    override int zeroLiteral(BackendType type) {
        return addInstr((:zeroLiteral, type));
    }
    override int structLiteral(BackendType struct_, int[] regs) {
        [useReg(reg) for reg in regs];
        // FIXME structLiteral should take BackendStructType
        if (auto struct_ = struct_.instanceOf(BackendStructType))
            return addInstr((:structLiteral, struct_, regs));
        assert(false);
    }
    override int symbolList(string name) {
        return addInstr((:symbolList, name));
    }
    override int globalVar(string name, BackendType type, bool define, bool threadlocal) {
        return addInstr((:globalVar, name, type, define, threadlocal));
    }
    override int binop(string op, BackendType type, int left, int right) {
        useReg(left); useReg(right);
        return addInstr((:binOp, op, type, left, right));
    }
    override int bitcast(int from, BackendType to) {
        useReg(from);
        return addInstr((:bitCast, from, to));
    }
    override int zeroExtend(int value, BackendType to) {
        useReg(value);
        return addInstr((:zeroExtend, value, to));
    }
    override int signExtend(int value, BackendType to) {
        useReg(value);
        return addInstr((:signExtend, value, to));
    }
    override int trunc(int value, BackendType to) {
        useReg(value);
        return addInstr((:trunc, value, to));
    }
    override int convert(int from, BackendType to) {
        useReg(from);
        return addInstr((:convert, from, to));
    }
    override int call(BackendType retType, string name, int[] args) {
        [useReg(arg) for arg in args];
        return addInstr((:callInstr, retType, name, args));
    }
    override int getFuncPtr(BackendType type, string name) {
        return addInstr((:getFuncPtr, type, name));
    }
    override int callFuncPtr(BackendType type, int callReg, int[] args) {
        useReg(callReg);
        [useReg(arg) for arg in args];
        return addInstr((:callFuncPtr, type, callReg, args));
    }
    override int load(BackendType backendType, int source) {
        useReg(source);
        return addInstr((:load, backendType, source));
    }
    override int staticAlloca(BackendType backendType) {
        return addInstr((:staticAlloca, backendType));
    }
    override int field(BackendType backendType, int source, int index) {
        useReg(source);
        return addInstr((:field, backendType, source, index));
    }
    override int fieldOffset(BackendType backendType, int source, size_t index) {
        useReg(source);
        return addInstr((:fieldOffset, backendType, source, index));
    }
    override int ptrOffset(BackendType backendType, int ptrReg, int offsetReg) {
        useReg(ptrReg);
        useReg(offsetReg);
        return addInstr((:ptrOffset, backendType, ptrReg, offsetReg));
    }
    override void store(BackendType backendType, int targetReg, int valueReg) {
        useReg(targetReg);
        useReg(valueReg);
        addInstr((:store, backendType, targetReg, valueReg));
    }
    override void ret(int reg) {
        useReg(reg);
        addBlockEnderInstr((:return_, reg));
    }
    override void branch(string label) {
        addBlockEnderInstr((:branch, label));
        this.blockId = this.blockId + 1;
    }
    override void testBranch(int reg, string thenLabel, string elseLabel) {
        useReg(reg);
        addBlockEnderInstr((:testBranch, reg, thenLabel, elseLabel));
        this.blockId = this.blockId + 1;
    }
    override void switch_(int reg, (int value, string label, bool unlikely)[] targets, string defaultLabel) {
        useReg(reg);
        addBlockEnderInstr((:switch_, reg, targets, defaultLabel));
        this.blockId = this.blockId + 1;
    }
    void flush() {
        string valueFormat(Value value, bool parens) {
            import package(compiler).std.string : endsWith;

            if (value.type == "") return value.value;
            if (value.exactType) return "($(value.value))" if parens else value.value;
            if (value.value == "0" && value.type.endsWith("*"))
                return "NULL";
            string castExpr = "(" ~ value.type ~ ") " ~ value.value;
            if (parens) return "(" ~ castExpr ~ ")";
            else return castExpr;
        }
        string reg2(int reg, string type) {
            auto ssaInstr = this.instrs[reg];
            if (ssaInstr.mode == SSAMode.held || ssaInstr.mode == SSAMode.persisted) {
                if (ssaInstr.mode == SSAMode.held) this.instrs[reg].mode = SSAMode.done;

                mut auto value = ssaInstr.heldValue;
                if (type != value.type) value = Value(type, value.value, exactType=false);

                return valueFormat(value, true);
            }
            assert(false);
        }
        string reg(int reg) {
            return reg2(reg, this.instrs[reg].heldValue.type);
        }
        string unsignedReg(int target) {
            if (this.instrs[target].heldValue.type == "char")
                return "(unsigned char) " ~ reg(target);
            else
                return "(unsigned) " ~ reg(target);
        }
        Value regExt(int reg) {
            auto ssaInstr = this.instrs[reg];
            if (ssaInstr.mode == SSAMode.held) {
                this.instrs[reg].mode = SSAMode.done;
                return ssaInstr.heldValue;
            }
            assert(ssaInstr.mode == SSAMode.persisted);
            return ssaInstr.heldValue;
        }
        Value dispatch(int regId) {
            auto ssaInstr = this.instrs[regId];
            void flushRegs() {
                flushRegsImpl(regId);
            }
            ssaInstr.instr.case {
                (:arg, int index): {
                    if (this.args[index].size == 0) return Value.none;
                    string typestr = mod.typeFmt(this.args[index]);
                    return Value(typestr, "arg" ~ itoa(index), exactType=false);
                }
                (:intLiteral, long value): {
                    return Value("int", ltoa(value), exactType=true);
                }
                (:shortLiteral, int value): {
                    return Value("short", itoa(value), exactType=true);
                }
                (:byteLiteral, int value): {
                    return Value("char", itoa(value), exactType=true);
                }
                (:longLiteral, long value): {
                    return Value("long long int", ltoa(value), exactType=true);
                }
                (:floatLiteral, float value): {
                    return Value("float", ftoa(value), exactType=true);
                }
                (:doubleLiteral, double value): {
                    return Value("double", ftoa(value), exactType=true);
                }
                (:wordLiteral, Platform platform, long value): {
                    return Value("int", ltoa(value), exactType=true);
                }
                (:stringLiteral, string text): {
                    mut string escapedText;
                    // TODO ord()
                    for (i in 0 .. text.length) {
                        if (text[i .. i + 1] == "\"") escapedText ~= "\\\"";
                        else if (text[i .. i + 1] == "\\") escapedText ~= "\\\\";
                        else if (text[i .. i + 1] == "\r") escapedText ~= "\\r";
                        else if (text[i .. i + 1] == "\n") escapedText ~= "\\n";
                        else if (text[i .. i + 1] == "\0") escapedText ~= "\\0";
                        else escapedText ~= text[i .. i + 1];
                    }
                    return Value("char*", "\"" ~ escapedText ~ "\"", exactType=true);
                }
                (:zeroLiteral, BackendType type): {
                    if (type.size == 0) return Value.none;
                    string typestr = mod.typeFmt(type);
                    mut string init = "0";
                    if (type.instanceOf(BackendStructType) || type.instanceOf(BackendSpacerType))
                        init = "{0}";
                    return Value(typestr, init, exactType=false);
                }
                (:structLiteral, BackendStructType struct_, int[] regs): {
                    if (struct_.size == 0) return Value.none;
                    string typestr = mod.typeFmt(struct_);

                    mut string res;
                    for (int i, member in struct_.members) {
                        if (member.size == 0)
                            continue;
                        if (!res.empty) res ~= ", ";
                        res ~= reg(regs[i]);
                    }
                    return Value(typestr, "{" ~ res ~ "}", exactType=false);
                }
                (:symbolList, string name): {
                    return Value("void*", name, exactType=true);
                }
                (:globalVar, string name, BackendType type, bool define, bool threadlocal): {
                    if (define) mod.defineVar(name, type, threadlocal);
                    else mod.declareVar(name, type, threadlocal);
                    return Value(mod.typeFmt(new BackendPointerType(type, mod.platform)), "&$name", exactType=true);
                }
                (:binOp, string op, BackendType type, int left, int right): {
                    mut string typestr = mod.typeFmt(type);
                    if (isBooleanOp(op)) {
                        typestr = "int";
                    }
                    if (op == ">>>") {
                        return Value(typestr, "(" ~ unsignedReg(left) ~ " >> " ~ reg(right) ~ ")", exactType=true);
                    } else if (op == "%") {
                        auto l = reg(left), r = reg(right);
                        return Value(typestr, "((($l % $r) + $r) % $r)", exactType=true);
                    } else {
                        return Value(typestr, "(" ~ reg(left) ~ " " ~ op ~ " " ~ reg(right) ~ ")", exactType=true);
                    }
                }
                (:bitCast, int from, BackendType to): {
                    auto from = regExt(from);
                    if (from.fake) return Value.none;
                    string typestr = mod.typeFmt(to);

                    return Value(typestr, from.value, exactType=false);
                }
                (:zeroExtend, int value, BackendType to): {
                    string type = mod.typeFmt(to);
                    return Value(type, "($type) $(unsignedReg(value))", exactType=true);
                }
                (:signExtend, int value, BackendType to): {
                    // TODO revisit when we have unsigned types
                    string type = mod.typeFmt(to);
                    return Value(type, "($type) $(reg(value))", exactType=true);
                }
                (:trunc, int value, BackendType to): {
                    string type = mod.typeFmt(to);
                    return Value(type, "($type) $(reg(value))", exactType=true);
                }
                (:convert, int from, BackendType to): {
                    auto from = regExt(from);
                    string toStr = mod.typeFmt(to);
                    if (from.type == "int")
                        // impl conv
                        return Value(toStr, from.value, exactType=true);
                    else
                        // cast conv
                        return Value(toStr, from.value, exactType=false);
                }
                (:callInstr, BackendType retType, string name, int[] args): {
                    auto fpType = mod.globals.type(name).instanceOf(BackendFunctionPointerType)? else die;
                    // Note: this *must* come first to ensure that the argstring doesn't double eval struct values.
                    flushRegs;
                    auto argstr = createArgString(fpType, args);
                    if (retType.size == 0) {
                        return Value("", name ~ "(" ~ argstr ~ ")", exactType=true);
                    }
                    string type = mod.typeFmt(retType);
                    return Value(type, name ~ "(" ~ argstr ~ ")", exactType=true);
                }
                (:getFuncPtr, BackendType type, string name): {
                    return Value(mod.typeFmt(type), name, exactType=true);
                }
                (:callFuncPtr, BackendType type, int regId, int[] args): {
                    auto backendFpType = type.instanceOf(BackendFunctionPointerType)? else die;
                    auto retType = backendFpType.ret;
                    // Note: this *must* come first to ensure that the argstring doesn't double eval struct values.
                    flushRegs;
                    auto argstr = createArgString(backendFpType, args);
                    string target = reg(regId);
                    if (retType.size == 0) {
                        return Value("", "(" ~ target ~ ")(" ~ argstr ~ ")", exactType=true);
                    }
                    string typestr = mod.typeFmt(retType);
                    return Value(typestr, "(" ~ target ~ ")(" ~ argstr ~ ")", exactType=true);
                }
                (:load, BackendType type, int regId): {
                    import package(compiler).std.string : startsWith;
                    if (type.size == 0) return Value.none;
                    string type = mod.typeFmt(type);
                    string value = reg(regId);
                    if (value.startsWith("&")) {
                        return Value(type, value[1 .. $], exactType=true);
                    } else {
                        return Value(type, "*" ~ value, exactType=true);
                    }
                }
                (:staticAlloca, BackendType type): {
                    flushRegs; // TODO is this right?
                    if (type.size == 0) return Value("", "NULL", exactType=false);
                    string typefmt = mod.typeFmt(type);
                    mut string init = "0";
                    if (type.instanceOf(BackendStructType) || type.instanceOf(BackendSpacerType))
                        init = "{0}";
                    this.print(typefmt ~ " reg" ~ itoa(regId) ~ "_frame = " ~ init ~ ";");
                    return Value(typefmt ~ "*", "&reg" ~ itoa(regId) ~ "_frame", exactType=true);
                }
                (:field, BackendType type, int regId, int index): {
                    auto backendStructType = type.instanceOf(BackendStructType);
                    assert(!!backendStructType);

                    if (backendStructType.members[index].size == 0) return Value.none;
                    string typefmt = mod.typeFmt(backendStructType.members[index]);
                    return Value(typefmt, "(" ~ reg(regId) ~ ")._" ~ ltoa(index), exactType=true);
                }
                (:fieldOffset, BackendType type, int regId, size_t index): {
                    auto backendStructType = type.instanceOf(BackendStructType);
                    assert(!!backendStructType);

                    if (backendStructType.members[index].size == 0) return Value("", "NULL", exactType=false);
                    string typefmt = mod.typeFmt(backendStructType) ~ "*";
                    string memberfmt = mod.typeFmt(backendStructType.members[index]);
                    return Value(memberfmt ~ "*", "&(" ~ reg2(regId, typefmt) ~ ")->_" ~ ltoa(index), exactType=true);
                }
                (:ptrOffset, BackendType type, int ptrReg, int offsetReg): {
                    // zero-size pointer math is undefined and probably unimportant
                    if (type.size == 0) return Value("", "NULL", exactType=false);
                    string typefmt = mod.typeFmt(type) ~ "*";
                    return Value(
                        typefmt,
                        "&(" ~ reg2(ptrReg, typefmt) ~ ")"
                            ~ "[" ~ reg(offsetReg) ~ "]",
                        exactType=true);
                }
                (:store, BackendType type, int targetReg, int valueReg): {
                    if (type.size == 0) return Value.none;
                    auto targetRegExt = regExt(targetReg);
                    auto valueRegStr = reg(valueReg);
                    if (valueRegStr == "") {
                        .print("storing invalid register $(valueReg) of $(type.repr) in $targetReg");
                        mut int* i; *i = *i;
                    }
                    flushRegs;
                    string typeStr = mod.typeFmt(type);
                    if (targetRegExt.type == typeStr) {
                        this.print("*" ~ targetRegExt.value ~ " = " ~ valueRegStr ~ ";");
                    } else {
                        this.print("*(" ~ typeStr ~ "*) " ~ targetRegExt.value
                            ~ " = " ~ valueRegStr ~ ";");
                    }
                    return Value.none;
                }
                (:return_, int regId): {
                    if (this.ret_.size == 0) {
                        flushRegs;
                        this.print("return;");
                        return Value.none;
                    }
                    auto val = reg(regId);
                    if (val == "") {
                        .print("Backend error: $(this.name): nonzero return with fake register.");
                        auto ssaInstr = this.instrs[regId];
                        .print("reg $regId, value $(ssaInstr.heldValue.repr) from $(ssaInstr.instr.repr)");
                        assert(false);
                    }
                    this.print("return " ~ val ~ ";");
                    flushRegs;
                    return Value.none;
                }
                (:setLabel, string label): {
                    flushRegs;
                    this.print(label ~ ":;");
                    return Value.none;
                }
                (:branch, string label): {
                    flushRegs;
                    this.print("goto " ~ label ~ ";");
                    return Value.none;
                }
                (:unreachable): {
                    this.print("/* noreturn */");
                    flushRegs;
                    return Value.none;
                }
                (:testBranch, int regId, string thenLabel, string elseLabel):
                    auto testReg = reg(regId);
                    flushRegs;
                    this.print("if (" ~ testReg ~ ") "
                        ~ "goto " ~ thenLabel ~ "; "
                        ~ "else goto " ~ elseLabel ~ ";");
                    return Value.none;
                (:switch_, int regId, (int value, string label, bool unlikely)[] targets, string defaultLabel):
                    auto testReg = reg(regId);
                    flushRegs;
                    this.print("switch ($testReg) {");
                    for (target in targets) {
                        this.print("  case $(target.value): goto $(target.label);");
                    }
                    this.print("  default: goto $(defaultLabel);");
                    this.print("}");
                    return Value.none;
                (:setLocation, BackendLocation loc):
                    this.print("#line $(loc.line+1) \"$(loc.file)\"");
                    return Value.none;
            }
        }
        string createArgString(BackendFunctionPointerType fpType, int[] args) {
            mut string argstr;
            void add(string arg) {
                if (!argstr.empty) argstr ~= ", ";
                argstr ~= arg;
            }
            void recurse(Value value, BackendType type) {
                if (value.fake) return;
                if (auto struct_ = type.instanceOf(BackendStructType)) {
                    if (struct_.internal && struct_.size > 16) {
                        for (i, member in struct_.members) {
                            auto value = Value(
                                mod.typeFmt(member),
                                valueFormat(value, parens=true) ~ "._$i",
                                exactType=true);
                            recurse(value, member);
                        }
                        return;
                    }
                }
                add(valueFormat(value, parens=false));
            }
            mut int[] regularArgs = args;
            if (fpType.variadic) {
                regularArgs = args[0 .. fpType.params.length];
            }
            [recurse(regExt(arg), fpType.params[i]) for i, arg in regularArgs];
            [add(valueFormat(regExt(arg), parens=false))
                for arg in args[regularArgs.length .. $]];

            return argstr;
        }
        void persistReg(int regId) {
            mut Value value;
            auto ssaInstr = this.instrs[regId];
            if (ssaInstr.mode == SSAMode.held) {
                value = ssaInstr.heldValue;
            } else {
                assert(ssaInstr.mode == SSAMode.pending);
                value = dispatch(regId);
            }
            this.instrs[regId].mode = SSAMode.persisted;
            if (value.type == "" && value.value == "NULL") {
                // NULL stays NULL, regardless.
                this.instrs[regId].heldValue = value;
            } else if (value.type == "" && value.value != "NULL") {
                if (value.value != "")
                    this.print(value.value ~ ";");
                /*else
                    this.print("// reg$regId = nothing ($(ssaInstr.instr.repr))");*/
                this.instrs[regId].heldValue = Value.none;
            } else {
                import package(compiler).std.string : startsWith;

                mut string init; init = value.value;
                if (!init.startsWith("{")) init = valueFormat(value, false);
                this.print(value.type ~ " reg" ~ itoa(regId) ~ " = " ~ init ~ ";");
                this.instrs[regId].heldValue = Value(value.type, "reg" ~ itoa(regId), exactType=true);
            }
        }
        void flushRegsImpl(size_t limit) {
            for (regId in 0 .. limit) {
                auto ssaInstr = this.instrs[regId];
                if (ssaInstr.mode == SSAMode.held) {
                    persistReg(cast(int) regId);
                }
            }
        }
        for (int regId, ssaInstr in this.instrs) {
            assert(ssaInstr.mode == SSAMode.pending);
            if (ssaInstr.uses == 1) {
                auto value = dispatch(regId);
                this.instrs[regId].mode = SSAMode.held;
                this.instrs[regId].heldValue = value;
            } else {
                persistReg(regId);
            }
        }
        flushRegsImpl(this.instrs.length);
    }
    override void done() {
        flush;
        mut string argstr = "(";
        this.print("/*unreachable*/");
        mut string[] reconstituteArgs;
        mut bool first = true;
        void add(string arg) {
            if (first) first = false;
            else argstr ~= ", ";
            argstr ~= arg;
        }
        void recurse(BackendType arg, string prefix) {
            if (arg.size == 0) return;
            if (auto struct_ = arg.instanceOf(BackendStructType)) {
                if (struct_.internal && struct_.size > 16) {
                    auto fields = ["$(prefix)_$i" for i, member in struct_.members where member.size > 0];
                    [recurse(member, "$(prefix)_$i") for i, member in struct_.members];
                    reconstituteArgs ~= "$(mod.typeFmt(arg)) $prefix = { " ~ fields.join(", ") ~ " };";
                    return;
                }
            }
            add("$(mod.typeFmt(arg)) $prefix");
        }
        [recurse(arg, "arg$i") for i, arg in this.args];
        argstr ~= ")";
        mut string qualifier;
        if (this.mod.platform.win64) {
            if (!this.mod.forMacro || !neat_runtime_symbol_defined_in_main(name)) {
                // export everything so it shows up in LoadLibrary(NULL).
                qualifier ~= "__declspec(dllexport) ";
            } else {
                // qualifier ~= "__declspec(dllimport) ";
                // You're emitting a function that you know is already defined in the executable. What?
                // This is extremely fishy  but we'll tolerate it. TODO?
                return; // just never print anything to `this.mod`.
            }
        }
        mut string retstr;
        if (this.ret_.size > 0) {
            retstr = mod.typeFmt(this.ret_);
        } else {
            retstr = "void";
        }
        this.mod.output.print("$qualifier$retstr $(this.name)$argstr {");
        for (reconLine in reconstituteArgs) {
            this.mod.output.print("  $reconLine");
        }
        auto funtext = this.output.text;
        this.mod.output.print(funtext);
        this.mod.output.print("}");
    }
}

class LineAppender
{
    mut char mut[] buffer;
    mut size_t used;
    this() {
        this.buffer = new char mut[](128);
    }
    void appends(string msg) {
        this.expand(this.used + msg.length);
        memcpy(&this.buffer[this.used], msg.ptr, msg.length);
        this.used = this.used + msg.length;
    }
    void appendi(int i) {
        this.appends(itoa(i)); // TODO
    }
    void expand(size_t newLength) {
        while (newLength > this.buffer.length)
        {
            auto newBuf = new char mut[](this.buffer.length * 2);
            for (i in 0 .. this.used)
                newBuf[i] = this.buffer[i];
            this.buffer = newBuf;
        }
    }
    string flush() {
        string result = new string(this.used);
        memcpy(result.ptr, this.buffer.ptr, this.used);
        this.used = 0;
        return result;
    }
}

class LineWriter
{
    mut string mut[] lines;
    mut int length;
    LineAppender line;
    this() {
        this.line = new LineAppender;
    }
    void print(string msg) {
        this.line.appends(msg);
        this.endl;
    }
    void appendsi(string s, int i)
    {
        this.line.appends(s);
        this.line.appendi(i);
    }
    void appendsis(string s1, int i, string s2)
    {
        this.line.appends(s1);
        this.line.appendi(i);
        this.line.appends(s2);
    }
    void append(string a)
    {
        this.line.appends(a);
    }
    void append2s(string a, string b)
    {
        this.line.appends(a);
        this.line.appends(b);
    }
    void append3s(string a, string b, string c)
    {
        this.line.appends(a);
        this.line.appends(b);
        this.line.appends(c);
    }
    void append5s(string a, string b, string c, string d, string e)
    {
        this.line.appends(a);
        this.line.appends(b);
        this.line.appends(c);
        this.line.appends(d);
        this.line.appends(e);
    }
    void append6s(string a, string b, string c, string d, string e, string f)
    {
        this.append5s(a, b, c, d, e);
        this.line.appends(f);
    }
    void endl() {
        this.line.appends("\n");
        this.append_(this.line.flush);
    }
    void append_(string line) {
        if (this.length == this.lines.length) {
            mut size_t newLen = this.lines.length * 2;
            if (newLen == 0) newLen = 1;
            auto newLines = new string mut[](newLen);
            for (i in 0 .. this.length) newLines[i] = this.lines[i];
            this.lines = newLines;
        }
        this.lines[this.length] = line;
        this.length = this.length + 1;
    }
    string text()
    {
        mut size_t sum;
        for (i in 0 .. this.length)
            sum += this.lines[i].length;
        char mut[] res = new char mut[](sum);
        mut size_t offset = 0;
        for (i in 0 .. this.length)
        {
            string line = this.lines[i];
            memcpy(&res[offset], line.ptr, line.length);
            offset += line.length;
        }
        assert(offset == sum);
        return res.freeze;
    }
}

void defineCIntrinsics(CBackendModule mod, Platform platform) {
    BackendType voidp = platform.voidp;
    BackendType sizet = platform.nativeWordType;
    BackendType void_ = new BackendVoidType;
    mod.declare("neat_runtime_alloc", voidp, [sizet], variadic=false);
    mod.declare("neat_runtime_refcount_set", void_, [voidp, sizet], variadic=false);
}

string repr(Instr instr) {
    return instr.case(
        (:arg, int index): "arg($index)",
        (:intLiteral, long value): "intLiteral($value)",
        (:shortLiteral, int value): "shortLiteral($value)",
        (:byteLiteral, int value): "byteLiteral($value)",
        (:longLiteral, long value): "longLiteral($value)",
        (:floatLiteral, float value): "floatLiteral($value)",
        (:doubleLiteral, double value): "doubleLiteral($value)",
        (:wordLiteral, Platform platform, long value): "wordLiteral($value)",
        (:stringLiteral, string text): "stringLiteral($text)",
        (:zeroLiteral, BackendType type): "zeroLiteral($(type.repr))",
        (:unreachable): "unreachable",
        (:structLiteral, BackendStructType struct_, int[] regs): "structLiteral",
        (:symbolList, string name): "symbolList($name)",
        (:globalVar, string name, BackendType type, bool define, bool threadlocal):
            "globalVar($name, $(cast(int) define), $(cast(int) threadlocal))",
        (:binOp, string op, BackendType type, int left, int right): "binOp($op, $left, $right)",
        (:bitCast, int from, BackendType to): "bitCast($from, $(to.repr))",
        (:zeroExtend, int value, BackendType to): "zeroExtend($value, $(to.repr))",
        (:signExtend, int value, BackendType to): "signExtend($value, $(to.repr))",
        (:trunc, int value, BackendType to): "trunc($value, $(to.repr))",
        (:convert, int from, BackendType to): "convert($from, $(to.repr))",
        (:callInstr, BackendType retType, string name, int[] args): "call($name)",
        (:getFuncPtr, BackendType type, string name): "getFuncPtr($(type.repr), $name)",
        (:callFuncPtr, BackendType type, int regId, int[] args): "callFuncPtr($regId)",
        (:load, BackendType type, int regId): "load($(type.repr), $regId)",
        (:staticAlloca, BackendType type): "staticAlloca($(type.repr))",
        (:field, BackendType type, int regId, int index): "field($(type.repr), $regId, $index)",
        (:fieldOffset, BackendType type, int regId, size_t index): "fieldOffset($(type.repr), $regId, $index)",
        (:ptrOffset, BackendType type, int ptrReg, int offsetReg): "ptrOffset($(type.repr), $ptrReg, $offsetReg)",
        (:store, BackendType type, int targetReg, int valueReg): "store($(type.repr), $targetReg, $valueReg)",
        (:return_, int regId): "return($(regId))",
        (:setLabel, string label): "setLabel($label)",
        (:branch, string label): "branch($label)",
        (:testBranch, int regId, string thenLabel, string elseLabel): "testBranch($regId, $thenLabel, $elseLabel)",
        (:switch_, int regId, (int value, string label, bool unlikely)[] targets, string defaultLabel): "switch($regId)",
        (:setLocation, BackendLocation loc): "setLocation($(loc.line + 1), \"$(loc.file)\")"
    );
}
