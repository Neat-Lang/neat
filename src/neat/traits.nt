// Very, very similar to neat.pragma. (Copypaste, why can't I quit you)
module neat.traits;

macro import package(compiler).std.macro.listcomprehension;

import helpers;
import neat.base;
import neat.expr;
import neat.types;
import neat.util;

(nullable ASTTrait | fail Error) parseTrait(Parser parser, LexicalContext lexicalContext)
{
    if (!parser.acceptIdentifier("__traits"))
        return null;
    parser.expectToken(TokenType.lparen)?;
    auto from = parser.from;
    auto trait = parser.parseIdentifier;
    auto locRange = parser.to(from);
    mut ASTSymbol[] args;
    while (parser.acceptToken(TokenType.comma)) {
        if (auto arg = lexicalContext.compiler.parseExpression(parser, lexicalContext)?)
            args ~= arg;
        else return parser.fail("expected expression argument");
    }
    parser.expectToken(TokenType.rparen)?;
    return new ASTTrait(trait, args, locRange);
}

class ASTTrait : ASTSymbol
{
    string name;

    ASTSymbol[] args;

    this(this.name, this.args, this.locRange) { }

    override (Symbol | fail Error) compile(Context context) {
        if (name == "hasMember") {
            this.locRange.assert(args.length == 2, "__traits(hasMember) expected two arguments")?;
            return .hasMember(
                context, args[0].compile(context)?, args[1].compile(context)?, locRange);
        }
        if (name == "versionSet") {
            this.locRange.assert(args.length == 1, "__traits(versionSet) expected one argument")?;
            return .versionSet(
                context, args[0].compile(context)?, locRange);
        }
        return this.locRange.fail("unknown trait: \"$name\"");
    }
}

(Symbol | fail Error) hasMember(Context context, Symbol value, Symbol member, LocRange locRange) {
    auto valueExpr = value.isExpressionImplCall(context, locRange)?;
    auto stringLit = member.instanceOf(StringLiteral);
    locRange.assert(!!valueExpr, "__traits(hasMember) expected expression as first argument")?;
    locRange.assert(!!stringLit, "__traits(hasMember) expected string literal as second argument")?;
    auto valueExpr = valueExpr.notNull;
    auto stringLit = stringLit.notNull;
    auto sym = valueExpr.type.accessMember(
        context, valueExpr, stringLit.text, Protection.private_, locRange)?;

    return context.compiler.boolLiteral(!!sym);
}

(Symbol | fail Error) versionSet(Context context, Symbol name, LocRange locRange) {
    auto nameLit = name.instanceOf(StringLiteral);
    locRange.assert(!!nameLit, "__traits(versionSet) expected string literal as argument")?;
    auto nameLit = nameLit.notNull;
    bool isSet = [any a == nameLit.text for a in context.platform.versions];

    return context.compiler.boolLiteral(isSet);
}
