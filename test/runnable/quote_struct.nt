module quote_struct;

macro import std.macro.quasiquoting;

import package(compiler).neat.base;
import package(compiler).neat.runtime;
import package(compiler).neat.struct_;

class ASTQuoteTest : ASTStatement
{
    ASTStatement child;

    this(this.child) { }

    override (StatementCompileResult | Error) compile(Context context) {
        auto childExpr = context.compiler.astStatementExpression(
            [child], context.compiler.astNumberLiteral(0));
        auto stmt = context.compiler.$stmt {
            struct Test
            {
                int i;
                int foo() { return i * i; }
            }
            $childExpr;
        };
        return stmt.compile(context);
    }
}

class QuoteTest : Macro
{
    this() { }
    override void apply(MacroArgs args) {
        auto args = args.instanceOf(ParseStatementArgs);
        if (args) {
            args.statement = this.parse(args.parser, args.lexicalContext);
        }
    }

    (nullable ASTStatement | Error) parse(Parser parser, LexicalContext lexicalContext) {
        auto compiler = lexicalContext.compiler;
        auto loc = parser.loc;

        parser.begin;
        if (!parser.accept("quotetest")) {
            parser.revert;
            return null;
        }
        parser.commit;
        auto stmt = compiler.parseStatement(parser, lexicalContext)?;
        return new ASTQuoteTest(stmt);
    }
}

void addTestMacro(MacroState macroState) {
    macroState.addMacro(new QuoteTest);
}

macro(addTestMacro);

void main() {
    quotetest {
        Test t = Test(5);
        assert(t.foo == 25);
    }
}
